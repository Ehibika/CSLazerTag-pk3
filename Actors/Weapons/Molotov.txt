
actor MolotovGun : BasicLazer
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 2
Weapon.SlotNumber 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "Molotov Gun"
weapon.ammotype2 "MolotovAmmo"
inventory.icon "BUSTI"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Altfire:
HSPR B 0 
"####" B 0 A_JumpIfNoAmmo("NoAmmo2")
//"####" B 0 A_JumpIfInventory("MolotovCooldownItem",1,"NoAmmo2")
goto Altfire.Aim

Altfire.Aim:
"####" B 1 A_FireCustomMissile("MolotovAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" B 0 A_Jumpif(CallACS("CSLT_ButtonCheck",BT_ATTACK)==1,"NoAmmo2")
"####" B 0 A_Refire("Altfire.Aim")
Goto Altfire.Toss

Altfire.Toss:
"####" FFGGG 1
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("Molotov",0,1,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" B 0 A_GiveInventory("AmmoStaller")
//"####" B 0 A_GiveInventory("MolotovCooldown")
"####" IJKLM 2
"####" B 0 A_Jump(256,"Ready")
goto Ready

NoAmmo2:
BUST B 3
BUST B 0 A_Refire("NoAmmo")
Goto Ready
}
}

Actor MolotovAmmo : Ammo {inventory.amount 1 inventory.maxamount 2}


Const Int IncArea = 250;

actor MolotovAimPath : GolfBallAimInstantStarter3 { Speed 35 Gravity 1.5}

actor Molotov 
{
PROJECTILE
damagetype "Explosion"
Obituary "$OB_Hyperbomb"
radius 4
height 4
scale 2.0
-NOGRAVITY
+DOOMBOuNCE
+BOUNCEONCEILINGS
+ALLOWBOUNCEONACTORS
+BOUNCEONACTORS
+MTHRUSPECIES
+USEBOUNCESTATE
+NOTARGETSWITCH
+NOEXPLODEFLOOR
+SLIDESONWALLS
+MBFBOUNCER
speed 35
gravity 1.5
Damage (0)
wallbouncefactor 1.0
BounceFactor 0.1
reactiontime 1
States
{
Spawn:
MINB A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
MINB A 1 A_SpawnItemEX("MolotovTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
Goto Spawn2

Bounce.Floor:
Bounce.Ceiling:
MINB A 0 A_ChangeFlag("USEBOUNCESTATE",false)
MINB A 1 A_SpawnItemEX("Molotov2",0,0,0,momx,momy,momz,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM,0)
stop

Death:
MINB A 0 A_SpawnItemEx("MolotovField")
HARD BCDE 2
stop
Crash:
MINB A 0 A_SpawnItemEx("MolotovField")
HARD BCDE 2
stop
XDeath:
HARD BCDE 2
stop
}
}

actor Molotov2 : Molotov
{
-USEBOUNCESTATE
-NOGRAVITY
States
{
Spawn:
MINB A 0 A_PlaySoundEX("weapon/Heat1","Soundslot6")
MINB A 0 A_CheckFloor("Death")
MINB A 1 A_CountDown
loop
SlowDown:
MINB A 0 A_ScaleVelocity(0.1)
MINB A 1 A_CountDown
Goto Spawn
	
Death:
MINB A 0 A_SpawnItemEx("MolotovField")
HARD BCDE 2
stop
Crash:
MINB A 0 A_SpawnItemEx("MolotovField")
HARD BCDE 2
stop
XDeath:
HARD BCDE 2
stop
}
}


actor MolotovField : BasicExplosion2
{
DamageType "Incinerate"
obituary "$OB_MOLOTOV"
ReactionTime 35
Args 0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCodeSyncTarget",1)
//TNT1 A 0 ACS_NamedExecuteAlways("GiveEntiTID",0,ToxEntiTID)
TNT1 A 5
TNT1 A 0 A_Explode(5,IncArea,1,0,IncArea)
Spawn2:
TNT1 A 1
TNT1 A 0 A_PlaySoundEx("weapon/WaveBurner","Weapon")
TNT1 AAAAAA 0 A_SpawnItemEx("IncinerateParticleFX",Random(-IncArea,IncArea),0,-(z-floorz)+random(0,5),0,0,6,Random(1,360),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SetArg(0,Args[0]+1)
TNT1 A 0 A_JumpIf(Args[0] >= 7,"Spawn3")
Goto Spawn2
Spawn3:
TNT1 A 0 A_Explode(5,IncArea,1,0,IncArea)
TNT1 A 0 A_SetArg(0,0)
TNT1 A 0 A_CountDown
Goto Spawn2
Death:
TNT1 A 0 
stop
}
}


actor IncinerateParticleFX : BasicClientside
{
//translation "192:192=218:218", "198:198=221:221"
Renderstyle "add"
scale 1.5
Alpha 0.7
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_SetScale(Frandom(1.0,2.0))
BRNF ABCDEFGH 1 A_ScaleVelocity(0.6)
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Effect Actors|------------------------------
//[+]========================================================================[+]
actor MolotovTrail : BasicClientside
{
scale 1.5
States
{
Spawn:
TNT1 A 0
TNT1 A 2
MINB IIIIII 1
MINB IIIIII 1 A_Fadeout
stop
}
}

//[+]========================================================================[+]
//------------------------------|Cooldown Actors|------------------------------
//[+]========================================================================[+]
actor MolotovCooler : BasicHelper // Coolers are a type of watcher actor that handle weapon cooldowns
{
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 1
TNT1 A 0 A_TakeFromTarget("MolotovCooldownItem",1)
TNT1 A 0 A_JumpIfInTargetInventory("MolotovCooldownItem",1,"Spawn")
Death:
DeathTake:
TNT1 A 0
stop
}
}

actor MolotovCooldown : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Giveinventory("MolotovCooldownItem",105)
TNT1 A 0 A_Spawnitemex("MolotovCooler")
stop
Nope:
//BUST B 0 A_PlaySoundEx("weapon/heat3","Item")
TNT1 A 0
stop
}
}

actor MolotovCooldownItem : inventory
{
inventory.amount 1
inventory.maxamount 105
}
