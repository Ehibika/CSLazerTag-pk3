/*=================================
The Gunslinger offers increased headshot damage at the expense of energy usage,
great for those confident in their aim, but may be harder to use due to it's worse
accuracy and slightly lower fire rate.
===================================*/

Const float Gunslinger_SPD = 7.8;
Const int Gunslinger_BloomRate = 20;
Const float Gunslinger_Recoil = 3.0;
Const float Gunslinger_SwayLeft = 3.1;
Const float Gunslinger_SwayRight = 3.1;
Const int Gunslinger_MoveError = 3;

Const int Gunslinger_Damage = 20;
Const int Gunslinger_FalloffRange = FalloffBase;
Const int Gunslinger_FalloffRate = F_FLR;

Const int Gunslinger_HeadShotDamage = 120;
Const int Gunslinger_HeadShotFalloffRange = FalloffBase+1000;
Const int Gunslinger_HeadShotFalloffRate = S_HFLR;

const float Gunslinger_MoveSpeed = 0.8;

actor Gunslinger : WepMasterBase
{
Weapon.AmmoUse 120
Weapon.SlotNumber 1
Inventory.Pickupmessage "$PU_GUNSLINGER"
Obituary "$OB_GUNSLINGER"
Tag "Gunslinger"
weapon.ammotype "BusterPower"
inventory.icon "WHICON6"
States
{
Spawn:
WICO F 1
loop
Ready:
HLBB A 1 
"####" "#" 0 A_Jump(256,"Ready.Core")
Goto Ready.Core
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
BUST B 1 A_Lower
Loop
Select:
//TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,Gunslinger_MoveSpeed*FVM)
HLBB AA 0 A_Raise
HLBB A 1
Loop

Fire:
"####" C 0 //A_GiveInventory("Fighter_RR",1) 
"####" A 0 A_GiveInventory("Gunslinger_FS",1)
"####" A 0 A_GiveInventory("Gunslinger_SS",1)
"####" A 0 A_JumpIfNoAmmo("NoAmmo")
"####" A 0 A_PlaySoundEx("weapon/HardBuster2","Weapon")
"####" B 0 A_FireCustomMissile("Gunslinger_Beam",CallACS("GetShotDeviation",Gunslinger_SPD*FVM,Gunslinger_MoveError)/FVMF,1,8,0)
"####" B 0 A_GiveInventory("BloomCount",Gunslinger_BloomRate) //Stat
"####" A 0 A_SetAngle(angle-(FRandom(-Gunslinger_SwayLeft,Gunslinger_SwayRight)),SPF_INTERPOLATE)
"####" A 0 A_SetPitch(pitch-(Gunslinger_Recoil),SPF_INTERPOLATE)
"####" B 0 A_TakeInventory("FiringSpeed",9)
Hold:
"####" C 1
"####" B 0 A_Refire
"####" CB 2
"####" A 1
Goto Ready

Hold.Bot:
"####" CB 2
Goto Ready


NoAmmo:
"####" "#" 1 A_JumpIfInventory("BusterPower",1000,"Fire")
"####" "#" 0 A_Refire("NoAmmo")
Goto Ready

Flash:
TNT1 A 1
stop
NoFlash:
TNT1 A 1
stop
}
}

actor GunslingerDropitem : DropItemBase
{
inventory.pickupmessage "$PU_Gunslinger"
States
{
Spawn:
WICO F -1
loop

Pickup.WepCheck:
DUST B 0 A_JumpIfinventory("Gunslinger",1,"No")
DUST B 0 A_Jump(256,"Pickup.Check")
goto Pickup.Check

Pickup.WepGive:
DUST B 0 ACS_NamedExecuteAlways("DropYourWeapons",0,WP_Gunslinger)
DUST B 0 A_GiveInventory("WepPickupBuffer",1)
DUST B 0 A_GiveInventory("Gunslinger",1)
TNT1 A 0 A_SelectWeapon("Gunslinger")
stop
}
}

actor Gunslinger_SS : PowerupGiver
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
Powerup.Duration 17
powerup.type "AmmoStaller"
}

actor Gunslinger_FS : PowerupGiver
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
Speed 0.8
Powerup.Duration 17
powerup.type "FiringSpeed"
}

actor Gunslinger_Beam : LazerTagBeam{args 1} //Penetration

actor Gunslinger_BeamHitBox : LazerTagBeamHitBox
{
states
{
HeadShotCheck:
TNT1 A 0 A_JumpIf(Checkclass("PlayerPawn",AAPTR_TRACER,true)&&z>=ACS_NamedExecuteWithResult("TF2_TracerZGet")+40,"DoHeadshot")
TNT1 A 0 A_SpawnItemEx("Gunslinger_BodyShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
DoHeadShot:
TNT1 A 0 A_SpawnItemEx("Gunslinger_HeadShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("PowerStrikeFX",0,0,0,0,0,0,0,1)
stop
}
}

actor Gunslinger_BeamBurrow : LazerTagBeamBurrow
{
ReactionTime 20 //Penetration
States
{
Spawn.Reshoot:
LMEX A 0 A_SpawnItemEx("Gunslinger_WallBang",cos(pitch)*BurrowSpeed,0,(sin(pitch)*-BurrowSpeed),cos(pitch)*WallBangSpeed,0,(sin(pitch)*-WallBangSpeed),0,SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH|SXF_NOCHECKPOSITION)
stop

}
}

actor Gunslinger_WallBang : Gunslinger_Beam
{

States
{

Death:
AQBA L 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("TracerHealthGet")>0,"Headshotcheck")
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
HeadShotCheck:
TNT1 A 0 A_JumpIf(Checkclass("PlayerPawn",AAPTR_TRACER,true)&&z>=ACS_NamedExecuteWithResult("TF2_TracerZGet")+40,"DoHeadshot")
TNT1 A 0 A_SpawnItemEx("Gunslinger_WallBodyShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
DoHeadShot:
TNT1 A 0 A_SpawnItemEx("Gunslinger_WallHeadShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("PowerStrikeFX",0,0,0,0,0,0,0,1)
stop
}
}


actor Gunslinger_BodyShotter : TF2BodyShotter
{
Obituary "$OB_GUNSLINGER"
args 30
states
{

Spawn2:
TNT1 A 0 A_Explode(CallACS("DamageRate",Gunslinger_Damage,Gunslinger_FalloffRange,Gunslinger_FalloffRate),1,0,0,1)
TNT1 A 1
stop
}
}

actor Gunslinger_WallBodyShotter : Gunslinger_BodyShotter
{Obituary "$OB_WALLBANG"}

actor Gunslinger_HeadShotter : TF2HeadShotter
{
Obituary "$OB_GUNSLINGER2"
args 45
states
{
Spawn2:
TNT1 A 0 A_Explode(CallACS("DamageRate",Gunslinger_HeadShotDamage,Gunslinger_HeadShotFalloffRange,Gunslinger_HeadShotFalloffRate),1,0,0,1)
TNT1 A 1
stop
}
}

actor Gunslinger_WallHeadShotter : Gunslinger_HeadShotter
{Obituary "$OB_WALLHEADSHOT"}