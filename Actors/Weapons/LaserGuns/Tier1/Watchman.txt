/*=================================
The Watchman's low falloff and low bloom rate makes it a great weapon for playing at a distance, however
it's not a particularly mobile weapon, and takes longer to start regenerating energy.
===================================*/

Const float Watchman_SPD = 1.8;
Const int Watchman_BloomRate = 10;

Const float Watchman_Damage = 20;
Const float Watchman_FalloffRange = FalloffBase+1000;
Const float Watchman_FalloffRate = L_FLR;

Const int Watchman_HeadShotDamage = 30;
Const int Watchman_HeadShotFalloffRange = FalloffBase+1500;
Const int Watchman_HeadShotFalloffRate = L_HFLR;

const float Watchman_MoveSpeed = 1.0;

actor Watchman : WepMasterBase
{
Weapon.AmmoUse 35
Weapon.SlotNumber 1
Inventory.Pickupmessage "$PU_Watchman"
Obituary "$OB_WATCHMAN"
Tag "$TAG_WATCHMAN"
weapon.ammotype "BusterPower"
inventory.icon "WHICON3"
//Dropitem "WatchmanDropItem"
States
{
Spawn:
WICO C 1
loop
Ready:
HLBB A 1 
"####" "#" 0 A_Jump(256,"Ready.Core")
Goto Ready.Core
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
BUST B 1 A_Lower
Loop
Select:
//TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,Watchman_MoveSpeed*FVM)
HLBB AA 0 A_Raise
HLBB A 1
Loop

Fire:
"####" A 0 A_GiveInventory("Watchman_FS",1)
"####" A 0 A_GiveInventory("Watchman_SS",1)
"####" A 0 A_JumpIfNoAmmo("NoAmmo")
"####" A 0 A_PlaySoundEx("weapon/RapidBuster","Weapon")
"####" A 0 A_FireCustomMissile("Watchman_Beam",Frandom(-Watchman_SPD*CallACS("GetBloomRate")/100,Watchman_SPD*CallACS("GetBloomRate")/100),1,8,0)
"####" A 0 A_GiveInventory("BloomCount",Watchman_BloomRate) //Stat
"####" B 0 A_TakeInventory("FiringSpeed",9)
Hold:
"####" C 1
"####" B 0 A_Refire
"####" CB 2
Goto Ready

Hold.Bot:
"####" CB 2
Goto Ready

NoAmmo:
"####" "#" 1 A_JumpIfInventory("BusterPower",1000,"Fire")
"####" "#" 0 A_Refire("NoAmmo")
Goto Ready

Flash:
TNT1 A 1
stop
NoFlash:
TNT1 A 1
stop
}
}


actor WatchmanDropitem : DropItemBase
{
inventory.pickupmessage "$PU_Watchman"
States
{
Spawn:
WICO C -1
loop

Pickup.WepCheck:
DUST B 0 A_JumpIfinventory("Watchman",1,"No")
DUST B 0 A_Jump(256,"Pickup.Check")
goto Pickup.Check

Pickup.WepGive:
DUST B 0 ACS_NamedExecuteAlways("DropYourWeapons",0,WP_Watchman)
DUST B 0 A_GiveInventory("WepPickupBuffer",1)
DUST B 0 A_GiveInventory("Watchman",1)
TNT1 A 0 A_SelectWeapon("Watchman")
stop
}
}

actor Watchman_SS : PowerupGiver
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
Powerup.Duration 35
powerup.type "AmmoStaller"
}

actor Watchman_FS : PowerupGiver
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
Speed 0.6
Powerup.Duration 17
powerup.type "FiringSpeed"
}

actor Watchman_Beam : LazerTagBeam{
args 0
States
{
Spawn.Beam:
TNT1 A 0 //A_PlaySoundEx("weapon/Gemini","Weapon") //Silent
TNT1 A 0 A_SpawnItemEx("Watchman_BeamFX2",0,0,0,momx,momy,momz,0,9)
TNT1 A 0 A_Jump(256,"Spawn.Hitbox")
goto Spawn.Hitbox
}
} //Penetration

actor Watchman_BeamHitBox : LazerTagBeamHitBox
{
states
{
HeadShotCheck:
TNT1 A 0 A_JumpIf(Checkclass("PlayerPawn",AAPTR_TRACER,true)&&z>=ACS_NamedExecuteWithResult("TF2_TracerZGet")+40,"DoHeadshot")
TNT1 A 0 A_SpawnItemEx("Watchman_BodyShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
DoHeadShot:
TNT1 A 0 A_SpawnItemEx("Watchman_HeadShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("PowerStrikeFX",0,0,0,0,0,0,0,1)
stop
}
}

actor Watchman_BeamFX1 : LazerTagBeamFX1
{
Scale 2.0
States
{
Spawn:
AQBA L 0 A_GiveInventory("ProjectileCode",1)
ICEL I 2 
stop
}
}

actor Watchman_BeamFX2 : LazerTagBeamFX2
{
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("Watchman_BeamFX3",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("Watchman_BeamFX3",0,0,0,-momx*0.1,-momy*0.1,-momz*0.1,0,9)
TNT1 A 0 A_SpawnItemEx("Watchman_BeamFX3",0,0,0,-momx*0.2,-momy*0.2,-momz*0.2,0,9)
TNT1 A 0 A_SpawnItemEx("Watchman_BeamFX3",0,0,0,-momx*0.3,-momy*0.3,-momz*0.3,0,9)
TNT1 A 0 A_SpawnItemEx("Watchman_BeamFX3",0,0,0,-momx*0.4,-momy*0.4,-momz*0.4,0,9)
TNT1 A 0 A_SpawnItemEx("Watchman_BeamFX3",0,0,0,-momx*0.5,-momy*0.5,-momz*0.5,0,9)
TNT1 A 0 A_SpawnItemEx("Watchman_BeamFX3",0,0,0,-momx*0.6,-momy*0.6,-momz*0.6,0,9)
TNT1 A 0 A_SpawnItemEx("Watchman_BeamFX3",0,0,0,-momx*0.7,-momy*0.7,-momz*0.7,0,9)
TNT1 A 0 A_SpawnItemEx("Watchman_BeamFX3",0,0,0,-momx*0.8,-momy*0.8,-momz*0.8,0,9)
TNT1 A 0 A_SpawnItemEx("Watchman_BeamFX3",0,0,0,-momx*0.9,-momy*0.9,-momz*0.9,0,9)
TNT1 A 1 
stop
}
}

actor Watchman_BeamFX3 : LazerTagBeamFX3
{
Scale 2.0
States
{
Spawn:
ICEL I 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
ICEL I 0 A_Stop
ICEL I 1
ICEL I 1 A_Fadeout(0.25)
ICEL IIII 1 A_Fadeout(0.25)
//ICEL I 2
//ICEL IBBB 2
stop
}
}


actor Watchman_BeamBurrow : LazerTagBeamBurrow
{
States
{

Explode:
LROC A 0 A_Stop
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1
LROC A 0 A_PlaySoundEx("misc/kingswing", "Weapon")
LMEX A 0 A_SpawnItemEx("Watchman_WallBang",cos(pitch)*WallBangSpeed,0,(sin(pitch)*-WallBangSpeed),cos(pitch)*WallBangSpeed,0,(sin(pitch)*-WallBangSpeed),0,SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH|SXF_NOCHECKPOSITION)
stop

Repeate:
LMEX A 0 A_SpawnItemEx("Watchman_BeamBurrow",cos(pitch)*BurrowSpeed,0,(sin(pitch)*-BurrowSpeed),0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH|SXF_NOCHECKPOSITION,0,TID+1)
stop

}
}

actor Watchman_WallBang : Watchman_Beam
{

States
{


Death:
AQBA L 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("TracerHealthGet")>0,"Headshotcheck")
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
HeadShotCheck:
TNT1 A 0 A_JumpIf(Checkclass("PlayerPawn",AAPTR_TRACER,true)&&z>=ACS_NamedExecuteWithResult("TF2_TracerZGet")+40,"DoHeadshot")
TNT1 A 0 A_SpawnItemEx("Watchman_WallBodyShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
DoHeadShot:
TNT1 A 0 A_SpawnItemEx("Watchman_WallHeadShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("PowerStrikeFX",0,0,0,0,0,0,0,1)
stop
}
}


actor Watchman_BodyShotter : TF2BodyShotter
{
Obituary "$OB_WATCHMAN"
args 30
states
{

Spawn2:
TNT1 A 0 A_Explode(CallACS("DamageRate",Watchman_Damage,Watchman_FalloffRange,Watchman_FalloffRate),1,0,0,1)
TNT1 A 1
stop
}
}

actor Watchman_WallBodyShotter : Watchman_BodyShotter
{Obituary "$OB_WALLBANG"}

actor Watchman_HeadShotter : TF2HeadShotter
{
Obituary "$OB_WATCHMAN2"
args 45
states
{
Spawn2:
TNT1 A 0 A_Explode(CallACS("DamageRate",Watchman_HeadShotDamage,Watchman_HeadShotFalloffRange,Watchman_HeadShotFalloffRate),1,0,0,1)
TNT1 A 1
stop
}
}

actor Watchman_WallHeadShotter : Watchman_HeadShotter
{Obituary "$OB_WALLHEADSHOT"}