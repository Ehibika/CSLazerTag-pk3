/*=================================
The Scout is a fast firing blaster packed in a small package and price tag, struggles
at farther distances but can pack a real punch in the right circumstances.
===================================*/

Const float Scout_SPD = 4.2;
Const int Scout_BloomRate = 6;
Const float Scout_Recoil = 0.8;
Const float Scout_Kick = 0.8;
Const float Scout_SwayLeft = 1.5;
Const float Scout_SwayRight = 2.5;
Const int Scout_MoveError = 40;

Const int Scout_Damage = 10;
Const int Scout_FalloffRange = FalloffBase-800;
Const int Scout_FalloffRate = F_FLR;

Const int Scout_HeadShotDamage = 15;
Const int Scout_HeadShotFalloffRange = FalloffBase-600;
Const int Scout_HeadShotFalloffRate = F_HFLR;

const float Scout_MoveSpeed = 1.0;

actor Scout : WepMasterBase
{
Weapon.AmmoUse 65
Weapon.SlotNumber 2
Inventory.Pickupmessage "$PU_SCOUT"
Obituary "$OB_SCOUT"
Tag "Scout"
weapon.ammotype "BusterPower"
inventory.icon "WHICON4"
//Dropitem "ScoutDropItem"
States
{
Spawn:
WICO D 1
loop
Ready:
HLBB A 1 
"####" "#" 0 A_Jump(256,"Ready.Core")
Goto Ready.Core
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
BUST B 1 A_Lower
Loop
Select:
//TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,Scout_MoveSpeed*FVM)
HLBB A 1 A_Raise
Loop

Fire:
"####" C 0 A_GiveInventory("Scout_RR",1) 
"####" A 0 A_GiveInventory("Scout_FS",1)
"####" A 0 A_GiveInventory("Scout_SS",1)
"####" A 0 A_JumpIfNoAmmo("NoAmmo")
"####" A 0 A_PlaySoundEx("weapon/basshot","Weapon")
"####" A 0 A_FireCustomMissile("Scout_Beam",CallACS("GetShotDeviation",Scout_SPD*FVM,Scout_MoveError)/FVMF,1,8,0,0,CallACS("GetShotRecoil",Scout_Recoil*FVM)/FVMF)
"####" A 0 A_GiveInventory("BloomCount",Scout_BloomRate) //Stat
"####" A 0 A_SetAngle(angle-(FRandom(-Scout_SwayLeft,Scout_SwayRight)),SPF_INTERPOLATE)
"####" A 0 A_GiveInventory("Scout_REC",1) 
"####" BC 1
"####" AA 1
"####" A 0 A_Refire
"####" A 0 A_TakeInventory("FiringSpeed",9)
Goto Ready


NoAmmo:
"####" C 0 A_GiveInventory("Scout_RR",1) 
"####" A 0 A_TakeInventory("FiringSpeed",9)
NoAmmo2:
"####" "#" 1 A_JumpIfInventory("BusterPower",1000,"Fire")
"####" "#" 0 A_Refire("NoAmmo2")
Goto Ready


Flash:
TNT1 A 1
stop
NoFlash:
TNT1 A 1
stop
}
}

actor ScoutDropitem : DropItemBase
{
inventory.pickupmessage "$PU_Scout"
States
{
Spawn:
WICO D -1
loop

Pickup.WepCheck:
DUST B 0 A_JumpIfinventory("Scout",1,"No")
DUST B 0 A_Jump(256,"Pickup.Check")
goto Pickup.Check

Pickup.WepGive:
DUST B 0 ACS_NamedExecuteAlways("DropYourWeapons",0,WP_Scout)
DUST B 0 A_GiveInventory("WepPickupBuffer",1)
DUST B 0 A_GiveInventory("Scout",1)
TNT1 A 0 A_SelectWeapon("Scout")
stop
}
}

actor Scout_SS : PowerupGiver
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
Powerup.Duration 1
powerup.type "AmmoStaller"
}

actor Scout_FS : PowerupGiver
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
Speed 0.9
Powerup.Duration 8
powerup.type "FiringSpeed"
}


actor Scout_REC : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInventory("ShotCount", 6, "Nope")
DUST B 0 A_GiveInventory("ShotCount",1)
DUST B 0 A_GiveInventory("RRStaller",1)
TNT1 A 0 A_SetPitch(pitch-(Scout_Kick),SPF_INTERPOLATE)
stop

Nope:
DUST B 0 A_GiveInventory("RRStaller",1)
TNT1 A 0
Stop
}
}

actor Scout_RR : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInventory("RecoilRecover", 1, "Nope")
DUST B 0 A_GiveInventory("RecoilRecover",1)
DUST B 0 A_GiveInventory("RRStaller",1)
TNT1 A 0 A_SpawnItemEx("Scout_RRW")
stop
Nope:
DUST B 0 A_GiveInventory("RRStaller",1)
TNT1 A 0
Stop
}
}

actor Scout_RRC : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_SetPitch(pitch+(Scout_Kick),SPF_INTERPOLATE)
stop
}
}

actor Scout_RRW : RecoilRecoverBase 
{
States
{
Spawn.RR: //Recoil Recover
TNT1 A 0 A_GiveToTarget("Scout_RRC",1)
Goto Spawn.Take
}
}

actor Scout_Beam : LazerTagBeam{args 0} //Penetration

actor Scout_BeamHitBox : LazerTagBeamHitBox
{
states
{
HeadShotCheck:
TNT1 A 0 A_JumpIf(Checkclass("PlayerPawn",AAPTR_TRACER,true)&&z>=ACS_NamedExecuteWithResult("TF2_TracerZGet")+40,"DoHeadshot")
TNT1 A 0 A_SpawnItemEx("Scout_BodyShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
DoHeadShot:
TNT1 A 0 A_SpawnItemEx("Scout_HeadShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("PowerStrikeFX",0,0,0,0,0,0,0,1)
stop
}
}


actor Scout_BeamBurrow : LazerTagBeamBurrow
{
States
{

Explode:
LROC A 0 A_Stop
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1
LROC A 0 A_PlaySoundEx("misc/kingswing", "Weapon")
LMEX A 0 A_SpawnItemEx("Scout_WallBang",cos(pitch)*WallBangSpeed,0,(sin(pitch)*-WallBangSpeed),cos(pitch)*WallBangSpeed,0,(sin(pitch)*-WallBangSpeed),0,SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH|SXF_NOCHECKPOSITION)
stop

Repeate:
LMEX A 0 A_SpawnItemEx("Scout_BeamBurrow",cos(pitch)*BurrowSpeed,0,(sin(pitch)*-BurrowSpeed),0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH|SXF_NOCHECKPOSITION,0,TID+1)
stop

}
}

actor Scout_WallBang : Scout_Beam
{
ReactionTime 1
States
{
Spawn:
TNT1 A 0

Spawn2:
TNT1 A 0 A_PlaySoundEx("weapon/Gemini","Weapon")
TNT1 A 1 A_CountDown
loop

Death:
AQBA L 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("ScoutHealthGet")>0,"Headshotcheck")
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
HeadShotCheck:
TNT1 A 0 A_JumpIf(Checkclass("PlayerPawn",AAPTR_TRACER,true)&&z>=ACS_NamedExecuteWithResult("TF2_TracerZGet")+40,"DoHeadshot")
TNT1 A 0 A_SpawnItemEx("Scout_WallBodyShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
DoHeadShot:
TNT1 A 0 A_SpawnItemEx("Scout_WallHeadShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("PowerStrikeFX",0,0,0,0,0,0,0,1)
stop
}
}


actor Scout_BodyShotter : TF2BodyShotter
{
Obituary "$OB_SCOUT"
args 30
states
{

Spawn2:
TNT1 A 0 A_Explode(CallACS("DamageRate",Scout_Damage,Scout_FalloffRange,Scout_FalloffRate),1,0,0,1)
TNT1 A 1
stop
}
}

actor Scout_WallBodyShotter : Scout_BodyShotter
{Obituary "$OB_WALLBANG"}

actor Scout_HeadShotter : TF2HeadShotter
{
Obituary "$OB_SCOUT2"
args 45
states
{
Spawn2:
TNT1 A 0 A_Explode(CallACS("DamageRate",Scout_HeadShotDamage,Scout_HeadShotFalloffRange,Scout_HeadShotFalloffRate),1,0,0,1)
TNT1 A 1
stop
}
}

actor Scout_WallHeadShotter : Scout_HeadShotter
{Obituary "$OB_WALLHEADSHOT"}