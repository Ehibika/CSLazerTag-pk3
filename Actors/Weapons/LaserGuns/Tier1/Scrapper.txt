/*=================================
The Scrapper Boasts heightened fire rate compared to other tier 1 blasters while still
having decent damage, faster firing does mean faster bloom and energy usage however.
===================================*/

Const float Scrapper_SPD = 2.1;
Const int Scrapper_BloomRate = 8;
Const float Scrapper_Recoil = 1.0;
Const float Scrapper_SwayLeft = 1.1;
Const float Scrapper_SwayRight = 1.1;
Const int Scrapper_MoveError = 5;

Const int Scrapper_D1 = 30; // Highest damage
Const int Scrapper_R1 = 30; //in 32 MU Meters
Const int Scrapper_D2 = 20; //Mid damage
Const int Scrapper_R2 = 50; //in 32 MU Meters
Const int Scrapper_MinD = 10; //Lowest damage

Const int Scrapper_H_D1 = 35; // Highest damage
Const int Scrapper_H_R1 = 30; //in 32 MU Meters
Const int Scrapper_H_D2 = 25; //Mid damage
Const int Scrapper_H_R2 = 50; //in 32 MU Meters
Const int Scrapper_H_MinD = 20; //Lowest damage

const float Scrapper_MoveSpeed = 1.0;

actor Scrapper : WepMasterBase
{
Weapon.AmmoUse 75
Weapon.SlotNumber 1
Inventory.Pickupmessage "$PU_SCRAPPER"
Obituary "$OB_SCRAPPER"
Tag "Scrapper"
weapon.ammotype "BusterPower"
inventory.icon "WHICON2"
States
{
Spawn:
WICO B 1
loop
Ready:
CMU2 A 1 
"####" "#" 0 A_Jump(256,"Ready.Core")
Goto Ready.Core
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
BUST B 1 A_Lower
Loop
Select:
//TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
TNT1 A 0 ACS_NamedExecuteAlways("WepSetup",0,Scrapper_MoveSpeed*FVM,W_SideArm)
CMUA AA 0 A_Raise
CMUA A 1
Loop

Fire:
"####" A 0 A_GiveInventory("Scrapper_FS",1)
"####" A 0 A_GiveInventory("Scrapper_SS",1)
"####" A 0 A_JumpIfNoAmmo("NoAmmo")
"####" A 0 A_PlaySoundEx("weapon/basshot","Weapon")
"####" B 0 A_FireCustomMissile("Scrapper_Beam",CallACS("GetShotDeviation",Scrapper_SPD*FVM,Scrapper_MoveError)/FVMF,1,8,0)
"####" A 0 A_GiveInventory("BloomCount",Scrapper_BloomRate) //Stat
"####" A 0 A_SetAngle(angle-(FRandom(-Scrapper_SwayLeft,Scrapper_SwayRight)),SPF_INTERPOLATE)
"####" A 0 A_SetPitch(pitch-(Scrapper_Recoil),SPF_INTERPOLATE)
"####" B 0 A_TakeInventory("FiringSpeed",9)
"####" A 0 A_GiveInventory("ScrapperSpamSensor",1)
"####" B 1
Hold:
"####" D 1
"####" C 0 A_Refire
"####" C 1
Goto Ready


NoAmmo:
"####" A 1 A_JumpIfInventory("BusterPower",1000,"Fire")
"####" A 0 A_Refire("NoAmmo")
Goto Ready

Flash:
TNT1 A 1
stop
NoFlash:
TNT1 A 1
stop
}
}

actor ScrapperDropitem : DropItemBase
{
inventory.pickupmessage "$PU_Scrapper"
States
{
Spawn:
WICO B -1
loop

Pickup.WepCheck:
DUST B 0 A_JumpIfinventory("Scrapper",1,"No")
DUST B 0 A_Jump(256,"Pickup.Check")
goto Pickup.Check

Pickup.WepGive:
DUST B 0 ACS_NamedExecuteAlways("DropYourSidearm",0,WP_Scrapper)
DUST B 0 A_GiveInventory("WepPickupBuffer",1)
DUST B 0 A_GiveInventory("Scrapper",1)
TNT1 A 0 A_SelectWeapon("Scrapper")
stop
}
}

actor ScrapperSpamFlag : Powerup {inventory.amount 1 Powerup.Duration 7}

actor ScrapperSpamSensor : CustomInventory
{
states
{
Pickup:
DUST B 0 A_JumpIfinventory("ScrapperSpamFlag",1,"Spammer")
DUST B 0 A_GiveInventory("ScrapperSpamFlag",1)
stop

Spammer:
DUST B 0 A_TakeInventory("BusterPower",75)
DUST B 0 A_GiveInventory("ScrapperSpamFlag",1)
stop
}
}

actor Scrapper_SS : PowerupGiver
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
Powerup.Duration 17
powerup.type "AmmoStaller"
}

actor Scrapper_FS : PowerupGiver
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
Speed 0.8
Powerup.Duration 17
powerup.type "FiringSpeed"
}

actor Scrapper_Beam : LazerTagBeam{args 0} //Penetration

actor Scrapper_BeamHitBox : LazerTagBeamHitBox
{
states
{
HeadShotCheck:
TNT1 A 0 A_JumpIf(Checkclass("PlayerPawn",AAPTR_TRACER,true)&&z>=ACS_NamedExecuteWithResult("TF2_TracerZGet")+40,"DoHeadshot")
TNT1 A 0 A_SpawnItemEx("Scrapper_BodyShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
DoHeadShot:
TNT1 A 0 A_SpawnItemEx("Scrapper_HeadShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("PowerStrikeFX",0,0,0,0,0,0,0,1)
stop
}
}

actor Scrapper_BeamBurrow : LazerTagBeamBurrow
{
ReactionTime 10 //Penetration
States
{
Spawn.Reshoot:
LMEX A 0 A_SpawnItemEx("Scrapper_WallBang",cos(pitch)*BurrowSpeed,0,(sin(pitch)*-BurrowSpeed),cos(pitch)*WallBangSpeed,0,(sin(pitch)*-WallBangSpeed),0,SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH|SXF_NOCHECKPOSITION)
stop

}
}

actor Scrapper_WallBang : Scrapper_Beam
{

States
{

Death:
AQBA L 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("TracerHealthGet")>0,"Headshotcheck")
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
HeadShotCheck:
TNT1 A 0 A_JumpIf(Checkclass("PlayerPawn",AAPTR_TRACER,true)&&z>=ACS_NamedExecuteWithResult("TF2_TracerZGet")+40,"DoHeadshot")
TNT1 A 0 A_SpawnItemEx("Scrapper_WallBodyShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
DoHeadShot:
TNT1 A 0 A_SpawnItemEx("Scrapper_WallHeadShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("PowerStrikeFX",0,0,0,0,0,0,0,1)
stop
}
}


actor Scrapper_BodyShotter : TF2BodyShotter
{
Obituary "$OB_SCRAPPER"
args 30
states
{

Spawn2:
TNT1 A 0 A_Explode(CallACS("DMinMax",Scrapper_MinD,CallACS("DMGR",Scrapper_D1,Scrapper_R1,Scrapper_D2,Scrapper_R2)),1,0,0,1)
TNT1 A 1
stop
}
}

actor Scrapper_WallBodyShotter : Scrapper_BodyShotter
{Obituary "$OB_WALLBANG"}

actor Scrapper_HeadShotter : TF2HeadShotter
{
Obituary "$OB_SCRAPPER2"
args 45
states
{
Spawn2:
TNT1 A 0 A_Explode(CallACS("DMinMax",Scrapper_H_MinD,CallACS("DMGR",Scrapper_H_D1,Scrapper_H_R1,Scrapper_H_D2,Scrapper_H_R2)),1,0,0,1)
TNT1 A 1
stop
}
}

actor Scrapper_WallHeadShotter : Scrapper_HeadShotter
{Obituary "$OB_WALLHEADSHOT"}