/*=================================
The Enforcer boasts greater damage per shot than other blasters but takes more
energy. it recovers fairly quickly however.
===================================*/

Const float Enforcer_SPD = 1.5;
Const int Enforcer_BloomRate = 10;
Const float Enforcer_Recoil = 1.2;
Const float Enforcer_Kick = 1.4;
Const float Enforcer_SwayLeft = 5.5;
Const float Enforcer_SwayRight = 5.5;
Const int Enforcer_MoveError = 10;

Const int Enforcer_D1 = 50; // Highest damage
Const int Enforcer_R1 = 20; //in 32 MU Meters
Const int Enforcer_D2 = 45; //Mid damage
Const int Enforcer_R2 = 30; //in 32 MU Meters
Const int Enforcer_MinD = 25; //Lowest damage

Const int Enforcer_H_D1 = 65; // Highest damage
Const int Enforcer_H_R1 = 20; //in 32 MU Meters
Const int Enforcer_H_D2 = 60; //Mid damage
Const int Enforcer_H_R2 = 50; //in 32 MU Meters
Const int Enforcer_H_MinD = 30; //Lowest damage

const float Enforcer_MoveSpeed = 0.8;

actor Enforcer : WepMasterBase
{
Weapon.AmmoUse 50
Weapon.SlotNumber 2
Inventory.Pickupmessage "$PU_ENFORCER"
Obituary "$OB_ENFORCER"
Tag "Enforcer"
weapon.ammotype "BusterPower2"
inventory.icon "WHICON10"
//Dropitem "EnforcerDropItem"
States
{
Spawn:
WICO J 1
loop
Ready:
PIDB A 1 
"####" "#" 0 A_Jump(256,"Ready.Core")
Goto Ready.Core
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
BUST B 1 A_Lower
Loop
Select:
//TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
TNT1 A 0 ACS_NamedExecuteAlways("WepSetup",0,Enforcer_MoveSpeed*FVM,W_MainWeapon)
PIDB A 1 A_Raise
Loop

Fire:
"####" A 0 A_GiveInventory("Enforcer_RR",1) 
"####" A 0 A_GiveInventory("Enforcer_FS",1)
"####" A 0 A_GiveInventory("Enforcer_SS",1)
"####" A 0 A_JumpIfNoAmmo("NoAmmo")
"####" A 0 A_PlaySoundEx("weapon/RapidBuster3","Weapon")
"####" A 0 A_FireCustomMissile("Enforcer_Beam",CallACS("GetShotDeviation",Enforcer_SPD*FVM,Enforcer_MoveError)/FVMF,1,8,0,0,CallACS("GetShotRecoil",Enforcer_Recoil*FVM)/FVMF)
"####" A 0 A_GiveInventory("BloomCount",Enforcer_BloomRate) //Stat
//"####" A 0 A_SetAngle(angle-(FRandom(-Enforcer_SwayLeft,Enforcer_SwayRight)),SPF_INTERPOLATE)
"####" A 0 A_SetAngle(angle-(CallACS("LeftRightDeviation",-2,Enforcer_SwayLeft*FVM,Enforcer_SwayRight*FVM,2.2*FVM)/FVMF),SPF_INTERPOLATE)
"####" A 0 A_GiveInventory("Enforcer_REC",1) 
"####" BC 1
"####" A 3
"####" A 0 A_Refire
"####" A 0 A_TakeInventory("FiringSpeed",9)
Goto Ready


NoAmmo:
"####" C 0 A_GiveInventory("Enforcer_RR",1) 
"####" A 0 A_TakeInventory("FiringSpeed",9)
NoAmmo2:
"####" "#" 1 A_JumpIfInventory("BusterPower2",1000,"Fire")
"####" "#" 0 A_Refire("NoAmmo2")
Goto Ready

Flash:
TNT1 A 1
stop
NoFlash:
TNT1 A 1
stop
}
}

actor EnforcerDropitem : DropItemBase
{
inventory.pickupmessage "$PU_Enforcer"
States
{
Spawn:
WICO J -1
loop

Pickup.WepCheck:
DUST B 0 A_JumpIfinventory("Enforcer",1,"No")
DUST B 0 A_Jump(256,"Pickup.Check")
goto Pickup.Check

Pickup.WepGive:
DUST B 0 ACS_NamedExecuteAlways("DropYourMainWep",0,WP_Enforcer)
DUST B 0 A_GiveInventory("WepPickupBuffer",1)
DUST B 0 A_GiveInventory("Enforcer",1)
TNT1 A 0 A_SelectWeapon("Enforcer")
stop
}
}

actor Enforcer_SS : PowerupGiver
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
Powerup.Duration 8
powerup.type "AmmoStaller"
}

actor Enforcer_FS : PowerupGiver
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
Speed 0.5
Powerup.Duration 17
powerup.type "FiringSpeed"
}


actor Enforcer_REC : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInventory("ShotCount", 6, "Nope")
DUST B 0 A_GiveInventory("ShotCount",1)
DUST B 0 A_GiveInventory("RRStaller",1)
TNT1 A 0 A_SetPitch(pitch-(Enforcer_Kick),SPF_INTERPOLATE)
stop

Nope:
DUST B 0 A_GiveInventory("RRStaller",1)
TNT1 A 0
Stop
}
}

actor Enforcer_RR : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInventory("RecoilRecover", 1, "Nope")
DUST B 0 A_GiveInventory("RecoilRecover",1)
DUST B 0 A_GiveInventory("RRStaller",1)
TNT1 A 0 A_SpawnItemEx("Enforcer_RRW")
stop
Nope:
DUST B 0 A_GiveInventory("RRStaller",1)
TNT1 A 0
Stop
}
}

actor Enforcer_RRC : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_SetPitch(pitch+(Enforcer_Kick),SPF_INTERPOLATE)
stop
}
}

actor Enforcer_RRW : RecoilRecoverBase 
{
States
{
Spawn.RR: //Recoil Recover
TNT1 A 0 A_GiveToTarget("Enforcer_RRC",1)
Goto Spawn.Take
}
}

actor Enforcer_Beam : LazerTagBeam{args 1} //Penetration

actor Enforcer_BeamHitBox : LazerTagBeamHitBox
{
states
{
HeadShotCheck:
TNT1 A 0 A_JumpIf(Checkclass("PlayerPawn",AAPTR_TRACER,true)&&z>=ACS_NamedExecuteWithResult("TF2_TracerZGet")+40,"DoHeadshot")
TNT1 A 0 A_SpawnItemEx("Enforcer_BodyShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
DoHeadShot:
TNT1 A 0 A_SpawnItemEx("Enforcer_HeadShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("PowerStrikeFX",0,0,0,0,0,0,0,1)
stop
}
}


actor Enforcer_BeamBurrow : LazerTagBeamBurrow
{
States
{

Explode:
LROC A 0 A_Stop
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1
LROC A 0 A_PlaySoundEx("misc/kingswing", "Weapon")
LMEX A 0 A_SpawnItemEx("Enforcer_BeamBurrow",cos(pitch)*WallBangSpeed,0,(sin(pitch)*-WallBangSpeed),cos(pitch)*WallBangSpeed,0,(sin(pitch)*-WallBangSpeed),0,SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH|SXF_NOCHECKPOSITION)
stop

Repeate:
LMEX A 0 A_SpawnItemEx("Enforcer_BeamBurrow",cos(pitch)*BurrowSpeed,0,(sin(pitch)*-BurrowSpeed),0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH|SXF_NOCHECKPOSITION,0,TID+1)
stop

}
}

actor Enforcer_WallBang : Enforcer_Beam
{
ReactionTime 1
States
{
Spawn:
TNT1 A 0

Spawn2:
TNT1 A 0 A_PlaySoundEx("weapon/Gemini","Weapon")
TNT1 A 1 A_CountDown
loop

Death:
AQBA L 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("TracerHealthGet")>0,"Headshotcheck")
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
HeadShotCheck:
TNT1 A 0 A_JumpIf(Checkclass("PlayerPawn",AAPTR_TRACER,true)&&z>=ACS_NamedExecuteWithResult("TF2_TracerZGet")+40,"DoHeadshot")
TNT1 A 0 A_SpawnItemEx("Enforcer_WallBodyShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
DoHeadShot:
TNT1 A 0 A_SpawnItemEx("Enforcer_WallHeadShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("PowerStrikeFX",0,0,0,0,0,0,0,1)
stop
}
}


actor Enforcer_BodyShotter : TF2BodyShotter
{
Obituary "$OB_ENFORCER"
args 30
states
{

Spawn2:
TNT1 A 0 A_Explode(CallACS("DMinMax",Enforcer_MinD,CallACS("DMGR",Enforcer_D1,Enforcer_R1,Enforcer_D2,Enforcer_R2)),1,0,0,1)
TNT1 A 1
stop
}
}

actor Enforcer_WallBodyShotter : Enforcer_BodyShotter
{Obituary "$OB_WALLBANG"}

actor Enforcer_HeadShotter : TF2HeadShotter
{
Obituary "$OB_ENFORCER2"
args 45
states
{
Spawn2:
TNT1 A 0 A_Explode(CallACS("DMinMax",Enforcer_H_MinD,CallACS("DMGR",Enforcer_H_D1,Enforcer_H_R1,Enforcer_H_D2,Enforcer_H_R2)),1,0,0,1)
TNT1 A 1
stop
}
}

actor Enforcer_WallHeadShotter : Enforcer_HeadShotter
{Obituary "$OB_WALLHEADSHOT"}