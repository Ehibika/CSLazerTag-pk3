/*=================================
The Toujorprey isn't a hard hitting blaster but it's very ammo efficient, and reloads quickly.
===================================*/

Const float Toujorprey_SPD = 1.4;
Const int Toujorprey_BloomRate = 8;
Const float Toujorprey_Recoil = 1.0;
Const float Toujorprey_Kick = 0.8;
Const float Toujorprey_SwayLeft = 1.5;
Const float Toujorprey_SwayRight = 1.5;
Const int Toujorprey_MoveError = 15;

Const int Toujorprey_Damage = 25;
Const int Toujorprey_FalloffRange = FalloffBase+600;
Const int Toujorprey_FalloffRate = L_FLR;

Const int Toujorprey_HeadShotDamage = 30;
Const int Toujorprey_HeadShotFalloffRange = FalloffBase+600;
Const int Toujorprey_HeadShotFalloffRate = L_HFLR;

const float Toujorprey_MoveSpeed = 0.8;

actor Toujorprey : WepMasterBase
{
Weapon.AmmoUse 22
Weapon.SlotNumber 2
Inventory.Pickupmessage "$PU_TOUJORPREY"
Obituary "$OB_TOUJORPREY"
Tag "$TAG_TOUJORPREY"
weapon.ammotype "BusterPower"
inventory.icon "WHICON11"
//Dropitem "ToujorpreyDropItem"
States
{
Spawn:
WICO K 1
loop
Ready:
PIDB A 1 
"####" "#" 0 A_Jump(256,"Ready.Core")
Goto Ready.Core
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
BUST B 1 A_Lower
Loop
Select:
//TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,Toujorprey_MoveSpeed*FVM)
PIDB A 1 A_Raise
Loop

Fire:
"####" A 0 A_GiveInventory("ToujorPrey_RR",1)
"####" A 0 A_GiveInventory("Toujorprey_FS",1)
"####" A 0 A_GiveInventory("Toujorprey_SS",1)
"####" A 0 A_JumpIfNoAmmo("NoAmmo")
"####" A 0 A_PlaySoundEx("weapon/RapidBuster","Weapon")
"####" A 0 A_FireCustomMissile("ToujorPrey_Beam",CallACS("GetShotDeviation",ToujorPrey_SPD*FVM,ToujorPrey_MoveError)/FVMF,1,8,0,0,CallACS("GetShotRecoil",ToujorPrey_Recoil*FVM)/FVMF)
"####" A 0 A_GiveInventory("BloomCount",ToujorPrey_BloomRate) //Stat
"####" A 0 A_SetAngle(angle-(FRandom(-ToujorPrey_SwayLeft,ToujorPrey_SwayRight)),SPF_INTERPOLATE)
"####" A 0 A_GiveInventory("ToujorPrey_REC",1) 
"####" BC 1
"####" A 2
"####" A 0 A_Refire
"####" A 0 A_TakeInventory("FiringSpeed",9)
Goto Ready


NoAmmo:
"####" C 0 A_GiveInventory("ToujorPrey_RR",1) 
"####" A 0 A_TakeInventory("FiringSpeed",9)
NoAmmo2:
"####" "#" 1 A_JumpIfInventory("BusterPower",1000,"Fire")
"####" "#" 0 A_Refire("NoAmmo2")
Goto Ready

Flash:
TNT1 A 1
stop
NoFlash:
TNT1 A 1
stop
}
}

actor ToujorpreyDropitem : DropItemBase
{
inventory.pickupmessage "$PU_Toujorprey"
States
{
Spawn:
WICO K -1
loop

Pickup.WepCheck:
DUST B 0 A_JumpIfinventory("Toujorprey",1,"No")
DUST B 0 A_Jump(256,"Pickup.Check")
goto Pickup.Check

Pickup.WepGive:
DUST B 0 ACS_NamedExecuteAlways("DropYourWeapons",0,WP_Toujorprey)
DUST B 0 A_GiveInventory("WepPickupBuffer",1)
DUST B 0 A_GiveInventory("Toujorprey",1)
TNT1 A 0 A_SelectWeapon("Toujorprey")
stop
}
}

actor Toujorprey_SS : PowerupGiver
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
Powerup.Duration 17
powerup.type "AmmoStaller"
}

actor Toujorprey_FS : PowerupGiver
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
Speed 0.8
Powerup.Duration 17
powerup.type "FiringSpeed"
}

actor Toujorprey_REC : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInventory("ShotCount", 6, "Nope")
DUST B 0 A_GiveInventory("ShotCount",1)
DUST B 0 A_GiveInventory("RRStaller",1)
TNT1 A 0 A_SetPitch(pitch-(Toujorprey_Kick),SPF_INTERPOLATE)
stop

Nope:
DUST B 0 A_GiveInventory("RRStaller",1)
TNT1 A 0
Stop
}
}

actor Toujorprey_RR : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInventory("RecoilRecover", 1, "Nope")
DUST B 0 A_GiveInventory("RecoilRecover",1)
DUST B 0 A_GiveInventory("RRStaller",1)
TNT1 A 0 A_SpawnItemEx("Toujorprey_RRW")
stop
Nope:
DUST B 0 A_GiveInventory("RRStaller",1)
TNT1 A 0
Stop
}
}

actor Toujorprey_RRC : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_SetPitch(pitch+(Toujorprey_Kick),SPF_INTERPOLATE)
stop
}
}

actor Toujorprey_RRW : RecoilRecoverBase 
{
States
{
Spawn.RR: //Recoil Recover
TNT1 A 0 A_GiveToTarget("Toujorprey_RRC",1)
Goto Spawn.Take
}
}

actor Toujorprey_Beam : LazerTagBeam{
args 1
States
{
Spawn.Beam:
TNT1 A 0 //A_PlaySoundEx("weapon/Gemini","Weapon") //Silent
TNT1 A 0 A_SpawnItemEx("Watchman_BeamFX2",0,0,0,momx,momy,momz,0,9)
TNT1 A 0 A_Jump(256,"Spawn.Hitbox")
goto Spawn.Hitbox
}
} //Penetration

actor Toujorprey_BeamHitBox : LazerTagBeamHitBox
{
states
{
HeadShotCheck:
TNT1 A 0 A_JumpIf(Checkclass("PlayerPawn",AAPTR_TRACER,true)&&z>=ACS_NamedExecuteWithResult("TF2_TracerZGet")+40,"DoHeadshot")
TNT1 A 0 A_SpawnItemEx("Toujorprey_BodyShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
DoHeadShot:
TNT1 A 0 A_SpawnItemEx("Toujorprey_HeadShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("PowerStrikeFX",0,0,0,0,0,0,0,1)
stop
}
}


actor Toujorprey_BeamBurrow : LazerTagBeamBurrow
{
States
{

Explode:
LROC A 0 A_Stop
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1
LROC A 0 A_PlaySoundEx("misc/kingswing", "Weapon")
LMEX A 0 A_SpawnItemEx("Toujorprey_WallBang",cos(pitch)*WallBangSpeed,0,(sin(pitch)*-WallBangSpeed),cos(pitch)*WallBangSpeed,0,(sin(pitch)*-WallBangSpeed),0,SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH|SXF_NOCHECKPOSITION)
stop

Repeate:
LMEX A 0 A_SpawnItemEx("Toujorprey_BeamBurrow",cos(pitch)*BurrowSpeed,0,(sin(pitch)*-BurrowSpeed),0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH|SXF_NOCHECKPOSITION,0,TID+1)
stop

}
}

actor Toujorprey_WallBang : Toujorprey_Beam
{
States
{

Death:
AQBA L 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("TracerHealthGet")>0,"Headshotcheck")
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
HeadShotCheck:
TNT1 A 0 A_JumpIf(Checkclass("PlayerPawn",AAPTR_TRACER,true)&&z>=ACS_NamedExecuteWithResult("TF2_TracerZGet")+40,"DoHeadshot")
TNT1 A 0 A_SpawnItemEx("Toujorprey_WallBodyShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
DoHeadShot:
TNT1 A 0 A_SpawnItemEx("Toujorprey_WallHeadShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("PowerStrikeFX",0,0,0,0,0,0,0,1)
stop
}
}


actor Toujorprey_BodyShotter : TF2BodyShotter
{
Obituary "$OB_TOUJORPREY"
args 30
states
{

Spawn2:
TNT1 A 0 A_Explode(CallACS("DamageRate",Toujorprey_Damage,Toujorprey_FalloffRange,Toujorprey_FalloffRate),1,0,0,1)
TNT1 A 1
stop
}
}

actor Toujorprey_WallBodyShotter : Toujorprey_BodyShotter
{Obituary "$OB_WALLBANG"}

actor Toujorprey_HeadShotter : TF2HeadShotter
{
Obituary "$OB_TOUJORPREY2"
args 45
states
{
Spawn2:
TNT1 A 0 A_Explode(CallACS("DamageRate",Toujorprey_HeadShotDamage,Toujorprey_HeadShotFalloffRange,Toujorprey_HeadShotFalloffRate),1,0,0,1)
TNT1 A 1
stop
}
}

actor Toujorprey_WallHeadShotter : Toujorprey_HeadShotter
{Obituary "$OB_WALLHEADSHOT"}