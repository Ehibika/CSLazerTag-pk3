/*=================================
The LightCannon is the Standard and Cheapest of the Tier 2 blasters, which are
more like assault rifles.
===================================*/

Const float LightCannon_SPD = 2.2;
Const int LightCannon_BloomRate = 4;
Const float LightCannon_Recoil = 4.6;
Const float LightCannon_Kick = 0.4;
Const float LightCannon_SwayLeft = 2.2;
Const float LightCannon_SwayRight = 2.1;
Const int LightCannon_MoveError = 5;

Const int LightCannon_Damage = 20;
Const int LightCannon_FalloffRange = FalloffBase;
Const int LightCannon_FalloffRate = L_FLR;

Const int LightCannon_HeadShotDamage = 25;
Const int LightCannon_HeadShotFalloffRange = FalloffBase;
Const int LightCannon_HeadShotFalloffRate = L_HFLR;

const float LightCannon_MoveSpeed = 0.6;

actor LightCannon : WepMasterBase
{
Weapon.AmmoUse 15
Weapon.SlotNumber 2
Inventory.Pickupmessage "$PU_LIGHTCANNON"
Obituary "$OB_LIGHTCANNON"
Tag "$TAG_LIGHTCANNON"
weapon.ammotype "BusterPower"
inventory.icon "WHICON13"
//Dropitem "LightCannonDropItem"
States
{
Spawn:
WICO M 1
loop
Ready:
FAKR A 1 
"####" "#" 0 A_Jump(256,"Ready.Core")
Goto Ready.Core
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
BUST B 1 A_Lower
Loop
Select:
//TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,LightCannon_MoveSpeed*FVM)
FAKR A 1 A_Raise
Loop

Fire:
"####" C 0 A_GiveInventory("LightCannon_RR",1) 
"####" A 0 A_GiveInventory("LightCannon_FS",1)
"####" A 0 A_GiveInventory("LightCannon_SS",1)
"####" A 0 A_JumpIfNoAmmo("NoAmmo")
"####" A 0 A_PlaySoundEx("weapon/BasherShot","Weapon")
"####" A 0 A_FireCustomMissile("LightCannon_Beam",CallACS("GetShotDeviation",LightCannon_SPD*FVM,LightCannon_MoveError)/FVMF,1,8,0,0,CallACS("GetShotRecoil",LightCannon_Recoil*FVM)/FVMF)
"####" A 0 A_GiveInventory("BloomCount",LightCannon_BloomRate) //Stat
"####" A 0 A_SetAngle(angle-(FRandom(-LightCannon_SwayLeft,LightCannon_SwayRight)),SPF_INTERPOLATE)
"####" A 0 A_GiveInventory("LightCannon_REC",1) 
"####" A 0 A_JumpIfInventory("ShotCount", 6, "Fire.After1")
"####" A 0 A_JumpIfInventory("ShotCount", 3, "Fire.After2")
Goto Fire.After3

Fire.After1:
"####" EDB 1
"####" A 0 A_Refire
"####" A 0 A_TakeInventory("FiringSpeed",9)
Goto Ready

Fire.After2:
"####" EDB 1
"####" B 1
"####" A 0 A_Refire
"####" A 0 A_TakeInventory("FiringSpeed",9)
Goto Ready

Fire.After3:
"####" EDB 1
"####" B 2
"####" A 0 A_Refire
"####" A 0 A_TakeInventory("FiringSpeed",9)
Goto Ready

NoAmmo:
"####" C 0 A_GiveInventory("LightCannon_RR",1) 
"####" A 0 A_TakeInventory("FiringSpeed",9)
NoAmmo2:
"####" "#" 1 A_JumpIfInventory("BusterPower",1000,"Fire")
"####" "#" 0 A_Refire("NoAmmo2")
Goto Ready

Flash:
TNT1 A 1
stop
NoFlash:
TNT1 A 1
stop
}
}

actor LightCannonDropitem : DropItemBase
{
inventory.pickupmessage "$PU_LightCannon"
States
{
Spawn:
WICO M -1
loop

Pickup.WepCheck:
DUST B 0 A_JumpIfinventory("LightCannon",1,"No")
DUST B 0 A_Jump(256,"Pickup.Check")
goto Pickup.Check

Pickup.WepGive:
DUST B 0 ACS_NamedExecuteAlways("DropYourWeapons",0,WP_LightCannon)
DUST B 0 A_GiveInventory("WepPickupBuffer",1)
DUST B 0 A_GiveInventory("LightCannon",1)
TNT1 A 0 A_SelectWeapon("LightCannon")
stop
}
}

actor LightCannon_SS : PowerupGiver
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
Powerup.Duration 35
powerup.type "AmmoStaller"
}

actor LightCannon_FS : PowerupGiver
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
Speed 0.2
Powerup.Duration 17
powerup.type "FiringSpeed"
}


actor LightCannon_REC : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInventory("ShotCount", 6, "Nope")
DUST B 0 A_GiveInventory("ShotCount",1)
DUST B 0 A_GiveInventory("RRStaller",1)
TNT1 A 0 A_SetPitch(pitch-(LightCannon_Kick),SPF_INTERPOLATE)
stop

Nope:
DUST B 0 A_GiveInventory("RRStaller",1)
TNT1 A 0
Stop
}
}

actor LightCannon_RR : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInventory("RecoilRecover", 1, "Nope")
DUST B 0 A_GiveInventory("RecoilRecover",1)
DUST B 0 A_GiveInventory("RRStaller",1)
TNT1 A 0 A_SpawnItemEx("LightCannon_RRW")
stop
Nope:
DUST B 0 A_GiveInventory("RRStaller",1)
TNT1 A 0
Stop
}
}

actor LightCannon_RRC : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_SetPitch(pitch+(LightCannon_Kick),SPF_INTERPOLATE)
stop
}
}

actor LightCannon_RRW : RecoilRecoverBase 
{
States
{
Spawn.RR: //Recoil Recover
TNT1 A 0 A_GiveToTarget("LightCannon_RRC",1)
Goto Spawn.Take
}
}

actor LightCannon_Beam : LazerTagBeam{args 3} //Penetration

actor LightCannon_BeamHitBox : LazerTagBeamHitBox
{
states
{
XDeath:
TNT1 A 0 //A_RemoveMaster //Punches right through players
AQBA L 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("TracerHealthGet")>0,"Headshotcheck")
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop

HeadShotCheck:
TNT1 A 0 A_JumpIf(Checkclass("PlayerPawn",AAPTR_TRACER,true)&&z>=ACS_NamedExecuteWithResult("TF2_TracerZGet")+40,"DoHeadshot")
TNT1 A 0 A_SpawnItemEx("LightCannon_BodyShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
DoHeadShot:
TNT1 A 0 A_SpawnItemEx("LightCannon_HeadShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("PowerStrikeFX",0,0,0,0,0,0,0,1)
stop
}
}


actor LightCannon_BeamBurrow : LazerTagBeamBurrow
{
States
{

Explode:
LROC A 0 A_Stop
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1
LROC A 0 A_PlaySoundEx("misc/kingswing", "Weapon")
LMEX A 0 A_SpawnItemEx("LightCannon_WallBang",cos(pitch)*WallBangSpeed,0,(sin(pitch)*-WallBangSpeed),cos(pitch)*WallBangSpeed,0,(sin(pitch)*-WallBangSpeed),0,SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH|SXF_NOCHECKPOSITION)
stop

Repeate:
LMEX A 0 A_SpawnItemEx("LightCannon_BeamBurrow",cos(pitch)*BurrowSpeed,0,(sin(pitch)*-BurrowSpeed),0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH|SXF_NOCHECKPOSITION,0,TID+1)
stop

}
}

actor LightCannon_WallBang : LightCannon_Beam
{
States
{

Death:
AQBA L 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("TracerHealthGet")>0,"Headshotcheck")
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
HeadShotCheck:
TNT1 A 0 A_JumpIf(Checkclass("PlayerPawn",AAPTR_TRACER,true)&&z>=ACS_NamedExecuteWithResult("TF2_TracerZGet")+40,"DoHeadshot")
TNT1 A 0 A_SpawnItemEx("LightCannon_WallBodyShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
DoHeadShot:
TNT1 A 0 A_SpawnItemEx("LightCannon_WallHeadShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("PowerStrikeFX",0,0,0,0,0,0,0,1)
stop
}
}


actor LightCannon_BodyShotter : TF2BodyShotter
{
Obituary "$OB_LIGHTCANNON"
args 30
states
{

Spawn2:
TNT1 A 0 A_Explode(CallACS("DamageRate",LightCannon_Damage,LightCannon_FalloffRange,LightCannon_FalloffRate),1,0,0,1)
TNT1 A 1
stop
}
}

actor LightCannon_WallBodyShotter : LightCannon_BodyShotter
{Obituary "$OB_WALLBANG"}

actor LightCannon_HeadShotter : TF2HeadShotter
{
Obituary "$OB_LIGHTCANNON2"
args 45
states
{
Spawn2:
TNT1 A 0 A_Explode(CallACS("DamageRate",LightCannon_HeadShotDamage,LightCannon_HeadShotFalloffRange,LightCannon_HeadShotFalloffRate),1,0,0,1)
TNT1 A 1
stop
}
}

actor LightCannon_WallHeadShotter : LightCannon_HeadShotter
{Obituary "$OB_WALLHEADSHOT"}