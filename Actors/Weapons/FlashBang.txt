
actor FlashBangGun : BasicLazer
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 2
Weapon.SlotNumber 1
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "FlashBang Gun"
weapon.ammotype2 "FlashBangAmmo"
inventory.icon "BUSTI"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Altfire:
HSPR B 0 
"####" B 0 A_JumpIfNoAmmo("NoAmmo2")
//"####" B 0 A_JumpIfInventory("FlashBangCooldownItem",1,"NoAmmo2")
goto Altfire.Aim

Altfire.Aim:
"####" B 1 A_FireCustomMissile("FlashBangAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" B 0 A_Jumpif(CallACS("CSLT_ButtonCheck",BT_ATTACK)==1,"NoAmmo2")
"####" B 0 A_Refire("Altfire.Aim")
Goto Altfire.Toss

Altfire.Toss:
"####" FFGGG 1
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("FlashBang",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" B 0 A_GiveInventory("AmmoStaller")
//"####" B 0 A_GiveInventory("FlashBangCooldown")
"####" IJKLM 2
"####" B 0 A_Jump(256,"Ready")
goto Ready

NoAmmo2:
BUST B 3
BUST B 0 A_Refire("NoAmmo2")
Goto Ready
}
}

Actor FlashBangAmmo : Ammo {inventory.amount 1 inventory.maxamount 2}


actor FlashBangAimPath : GolfBallAimInstantStarter3 { Speed 35 Gravity 1.5}

actor FlashBang 
{
PROJECTILE
damagetype "Explosion"
Obituary "$OB_Hyperbomb"
radius 4
height 4
scale 2.0
-NOGRAVITY
+DOOMBOuNCE
+BOUNCEONCEILINGS
+ALLOWBOUNCEONACTORS
+BOUNCEONACTORS
+MTHRUSPECIES
+USEBOUNCESTATE
+NOTARGETSWITCH
+NOEXPLODEFLOOR
+SLIDESONWALLS
+MBFBOUNCER
speed 35
gravity 1.5
Damage (0)
wallbouncefactor 1.0
BounceFactor 0.3
reactiontime 17
States
{
Spawn:
MINB A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
MINB A 0 A_SpawnItemEX("FlashBangTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
MINB A 0 A_PlaySoundEX("weapon/Heat1","Soundslot6")
MINB A 1 A_CountDown
Goto Spawn2

Bounce.Floor:
Bounce.Ceiling:
MINB A 0 A_ChangeFlag("USEBOUNCESTATE",false)
MINB A 1 A_SpawnItemEX("FlashBang2",0,0,0,momx,momy,momz,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM,0)
stop

Death:
MINB A 0 A_SpawnItemEx("FlashBangField")
HARD BCDE 2
stop
Crash:
MINB A 0 A_SpawnItemEx("FlashBangField")
HARD BCDE 2
stop
XDeath:
HARD BCDE 2
stop
}
}

actor FlashBang2 : FlashBang
{
-USEBOUNCESTATE
-NOGRAVITY
States
{
Spawn:
MINB A 0 A_PlaySoundEX("weapon/Heat1","Soundslot6")
MINB A 0 A_CheckFloor("SlowDown")
MINB A 1 A_CountDown
loop
SlowDown:
MINB A 0 A_ScaleVelocity(0.1)
MINB A 1 A_CountDown
Goto Spawn
	
Death:
MINB A 0 A_SpawnItemEx("FlashBangField")
HARD BCDE 2
stop
Crash:
MINB A 0 A_SpawnItemEx("FlashBangField")
HARD BCDE 2
stop
XDeath:
HARD BCDE 2
stop
}
}


actor FlashBangField : BasicExplosion2
{
-SOLID
+NOGRAVITY
PROJECTILE
+DONTBLAST
+NOINTERACTION
damage 0
radius 1
height 1
damagetype "FlashStopper"
Obituary "$OB_FLASHBANG"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("FlashStopperFX",0,0,0)
TNT1 A 0 A_PlaySoundEx("weapon/flashstopper","Weapon")
//TNT1 A 0 A_GiveToTarget("FlashStopperFlash", 1)
TNT1 A 1 A_Explode(5, FlashRang,1)
TNT1 A 0 ACS_NamedExecuteAlways("core_flashstopperlight", 0, FlashRang)
TNT1 A 1
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Effect Actors|------------------------------
//[+]========================================================================[+]
actor FlashBangTrail : BasicClientside
{
scale 1.5
States
{
Spawn:
TNT1 A 0
TNT1 A 2
MINB IIIIII 1
MINB IIIIII 1 A_Fadeout
stop
}
}

//[+]========================================================================[+]
//------------------------------|Cooldown Actors|------------------------------
//[+]========================================================================[+]
actor FlashBangCooler : BasicHelper // Coolers are a type of watcher actor that handle weapon cooldowns
{
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 1
TNT1 A 0 A_TakeFromTarget("FlashBangCooldownItem",1)
TNT1 A 0 A_JumpIfInTargetInventory("FlashBangCooldownItem",1,"Spawn")
Death:
DeathTake:
TNT1 A 0
stop
}
}

actor FlashBangCooldown : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Giveinventory("FlashBangCooldownItem",105)
TNT1 A 0 A_Spawnitemex("FlashBangCooler")
stop
Nope:
//BUST B 0 A_PlaySoundEx("weapon/heat3","Item")
TNT1 A 0
stop
}
}

actor FlashBangCooldownItem : inventory
{
inventory.amount 1
inventory.maxamount 105
}
