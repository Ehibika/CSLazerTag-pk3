/*=================================
The Standard Melee, makes you go fast when equipped.
===================================*/

const float Knife_MoveSpeed = 1.4;

actor BasicKnife : WepMasterBase
{
Weapon.AmmoUse 0
Weapon.SlotNumber 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "Knife"
weapon.ammotype "BusterPower"
inventory.icon "SCLASI"
States
{
SpawnLoop:
WEAP X 1
loop
Ready:
MKNI B 1 
"####" "#" 0 A_Jump(256,"Ready.Core")
Goto Ready.Core
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
MKNI B 1 A_Lower
Loop
Select:
//TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,Knife_MoveSpeed*FVM)
MKNI BBB 0 A_Raise
MKNI B 1 A_WEAPONREADY(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOBOB)
Loop

Fire:
"####" A 0 A_PlaySoundEx("weapon/SlashClaw","Weapon")
"####" A 5
"####" A 0 A_FireCustomMissile("ScratchMelee",0,0,8,0)
"####" BCD 2
"####" E 17
"####" A 0 A_Refire
goto Ready

Flash:
TNT1 A 1
stop
NoFlash:
TNT1 A 1
stop

DeadLowered:
DUST B 0 ACS_NamedExecuteAlways("DropYourWeapons",0,-1)
stop
}
}


actor ScratchMelee : BasicProjectile
{
DamageType "Knife"
scale 1.5
Speed 48
Damage(35)
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TBLA GGGG 1 A_SpawnItemEx("ScratchTrail",-48,0,0,momx,momy,momz,0,9)
stop

Death:
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
NDFX A 0 A_PlaySoundEx("weapon/slashclawhit","Weapon")
NDFX A 3 A_SpawnItemEx("ScratchImpactFX",0,0,0,0,0,0,0,1)
stop
}
}

actor ScratchTrail : BasicExplosion
{
damagetype "Knife"
Args 10
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("ScratchTrailFX1",0,0,0,-momx*0.3,-momy*0.3,-momz*0.3,0,9)
TNT1 A 0 A_SpawnItemEx("ScratchTrailFX2",0,0,0,-momx*0.6,-momy*0.6,-momz*0.6,0,9)
TNT1 A 0 A_SpawnItemEx("ScratchTrailFX3",0,0,0,-momx*0.9,-momy*0.9,-momz*0.9,0,9)
stop
}
}

actor ScratchTrailFX1 : BasicClientside
{
scale 1.5
alpha 0.8
renderstyle "Translucent"
speed 7
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TBLA G 1 
stop
}
}

actor ScratchTrailFX2 : ScratchTrailFX1
{
alpha 0.6
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TBLA F 1 
stop
}
}

actor ScratchTrailFX3 : ScratchTrailFX1
{
alpha 0.4
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TBLA E 1 
stop
}
}



actor ScratchImpactFX : BasicClientside
{
scale 2.0
States
{
Spawn:
TNT1 A 0
SCLA HIJKL 1
stop
}
}



actor KnifeBackstabItem : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_SpawnItemEX("KnifeBackstab")
stop
}
}

const int IMMUNE_RADIUS = 1000;

actor KnifeBackstab
{
var int user_stabbed_angle;
var int user_stabber_angle;
var int user_stab_angle_dif;
Height 0
Radius 0
+NOINTERACTION
+NOCLIP
damagetype "KnifeCrit"
PROJECTILE
//+SERVERSIDEONLY
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TARGET) //target assigned to tracer tracer
TNT1 A 0 A_TransferPointer(AAPTR_TARGET, AAPTR_DEFAULT, AAPTR_TARGET, AAPTR_TARGET) //target's target assigned to target
TNT1 A 0 A_SetUserVar("user_stabbed_angle", angle)
//TNT1 A 0 A_LogInt(user_stabbed_angle)
TNT1 A 0 A_FaceTarget
TNT1 A 0 A_SetUserVar("user_stabber_angle", (angle+180)%360)
TNT1 A 0 A_SetUserVar("user_stab_angle_dif", abs(user_stabbed_angle-user_stabber_angle))
TNT1 A 0 A_JumpIf(user_stab_angle_dif>180,"Wrap")
goto BackstabCheck
Wrap:
TNT1 A 0 A_SetUserVar("user_stab_angle_dif", abs(user_stab_angle_dif-360))
BackstabCheck:
//TNT1 A 0 A_LogInt(user_stab_angle_dif)
TNT1 A 0 A_JumpIf(user_stab_angle_dif<90,"DoBackstab")
//TNT1 A 0 A_JumpIf(user_stab_angle_dif<45,"DoBackstab")
TNT1 A 0 //A_Log("Facestab")
stop
DoBackstab:
TNT1 A 0 A_Warp(AAPTR_TRACER,0,0,ACS_NamedExecuteWithResult("TracerHeightGet")/2,0,0)
TNT1 A 0 A_RadiusGive("KnifeBackstabImmune",IMMUNE_RADIUS,RGF_PLAYERS|RGF_MONSTERS|RGF_OBJECTS)
TNT1 A 0 A_TakeInventory("KnifeBackstabImmune",1,0,AAPTR_TRACER)
TNT1 A 0 A_Explode(150,1,0,0,1)
TNT1 A 0 A_SpawnItemEx("PowerStrikeFX",0,0,0,0,0,0,0,1)
//TNT1 A 0 A_PlaysoundEX("Weapon/Slashclawhit","Weapon")//A_Log("Backstab")
TNT1 A 5
stop
}
}


actor SpecialDamageImmune : PowerProtection
{
Powerup.Duration 0x7FFFFFFF
damagefactor "Normal", 1.0
}

actor KnifeBackstabImmune : SpecialDamageImmune
{
damagefactor "KnifeCrit", 0.0
}
