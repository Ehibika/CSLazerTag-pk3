
actor Ability3_Knux : AbilityMasterBase
{
Tag "Ability 3"
inventory.icon "BUBMINI"
States
{

Ready:
BBO2 A 0 
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",2)==1,"User2")
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",3)==1,"User3")
//==
"####" B 0 A_FireCustomMissile("KnuxMineAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" A 1 A_WeaponReady
goto ready

Fire:
"####" AB 2
"####" B 0 A_PlaySoundEx("weapon/oilshot","Weapon")
"####" B 0 A_FireCustomMissile("KnuxMineSpawner",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" B 1 A_TakeInventory("AbilityCharge_3",1)
"####" B 0 A_GiveInventory("AmmoStaller")
"####" BA 2
"####" M 0 A_Jump(256,"End")
goto End


AltFire:
"####" A 1
End:
"####" A 1 A_TakeInventory("AbilityActiveFlag",9)
"####" A 0 
Goto End
}
}
//====

actor UseAbility3_Knux : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("BubbleMine_Ability",1)
fail
}
}

actor BubbleMine_Ability : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("AbilityCharge_3",1,"Pickup2")
stop

Pickup2:
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,3)
stop
}
}
//=================
//=================
actor KnuxMineThrust : CustomInventory
{
states
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 ThrustThingZ(0,180,0,0)
stop

Nope:
TNT1 A 0
Fail
}
}

actor KnuxMineAimPath : GolfBallAimInstantStarter3 { Speed 25 Gravity 1.5}

actor KnuxMineSpawner   
{
+MISSILE
+NOTARGETSWITCH
+NOBLOOD
+DONTBLAST
+SKYEXPLODE
+ThruActors
+BOUNCEONWALLS
+BOUNCEONCEILINGS
Obituary "$OB_KnuxMINE"
radius 8
height 8
scale 2.5
speed 25
Gravity 1.5
states
{
Spawn:
BARW H 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
BARW H 1
goto spawn2
Death:
BARW H 0 A_SpawnitemEx("KnuxMine",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
stop
}
}

actor KnuxMine  
{
+MISSILE
+NOBLOOD
+DONTBLAST
+SEEKERMISSILE
+NOTARGETSWITCH
+SHOOTABLE
+NOGRAVITY
+NOCLIP
+LOOKALLAROUND
Obituary "$OB_KNUXMINE"
radius 8
height 8
speed 0
health 10
scale 2.5
Args 96 , 32
states
{
Spawn:
BARW J 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 D 0 ACS_NamedExecuteAlways("GiveM_EntiTID",0,10,2)
BARW I 1 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_TARGET, AAPTR_TARGET)
BARW I 10 
BARW J 1 A_PlaySoundEX("weapon/flashstopper","Weapon")
BARW IJIJIJ 3 A_FadeTo(0.3)
BARW J 0 
BARW J 0 A_Jump(256,"Scan")
Goto Scan
Scan:
BARW J 1 A_RearrangePointers(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_DEFAULT)
BARW J 0 A_JumpifTracercloser(Args[1],"KnuxThrust")
BARW J 0  A_LookEx(LOF_NOSOUNDCHECK, 1, Args[0], Args[0], 360, "Targetcheck")
BARW J 0 A_Jump(256,"Scan")
Goto Scan
Targetcheck:
BARW J 1 A_Jumpifcloser(Args[0],"Targetcheck2")
BARW J 0 A_Jump(256,"Scan")
Goto Scan
Targetcheck2:
BARW J 0 A_JumpIf(IsPointerEqual(AAPTR_TARGET,AAPTR_TRACER) == True, "Scan")//You Are The Owner
TNT1 A 1 A_Jumpif(CallACS("TrapSensorIdentify")==1,"Scan") //You Are A Teammate
Goto Explode
KnuxThrust:
BARW J 0 A_Spawnitemex("KnuxExplosionSelf",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
BARW J 0 A_GiveInventory("KnuxMineThrust",1,AAPTR_TRACER)
Goto Explode+7
Explode:
BARW J 0 ACS_NamedExecuteAlways("ObjectDeathTally",0)
BARW J 1 A_RearrangePointers(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_DEFAULT)
BARW J 0 A_Changeflag("SHOOTABLE",0)
BARW J 0 A_FadeTo(1.0,1.0)
BARW I 10 A_PlaySoundEX("weapon/BombAlarm","Soundslot6")
TNT1 A 0 A_StopsoundEX("Soundslot6")
TNT1 A 0 A_Spawnitemex("KnuxExplosion",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Spawnitemex("KnuxFoamCloud",0,0,10,10,0,0,90,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Spawnitemex("KnuxFoamCloud",0,0,10,10,0,0,180,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Spawnitemex("KnuxFoamCloud",0,0,10,10,0,0,270,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Spawnitemex("KnuxFoamCloud",0,0,10,10,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
TNT1 A 1
Stop
Death:
BARW J 0 ACS_NamedExecuteAlways("ObjectDeathTally",0)
Goto Perish
Perish:
BARW J 0 A_FadeTo(1.0,1.0)
HARD BCDE 3
BARW J 0
stop
}
}



actor KnuxExplosion 
{
PROJECTILE
+NOINTERACTION
+NOCLIP
+DONTBLAST
+FORCEXYBILLBOARD
Speed 0
//
Damagetype "KnuxMine"
Obituary "$OB_KNUXMINE"
radius 8
height 8
scale 3.5
States
{
Spawn:
NAPC A 0 
NAPC D 0 A_Explode(25, 128, 1,0,80)
NAPC A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
ASEX ABCDEF 1 
stop
}
}

actor KnuxExplosionSelf : KnuxExplosion
{
States
{
Spawn:
NAPC A 0 
NAPC D 0 A_Explode(5, 128, 1,0,80)
NAPC A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
ASEX ABCDEF 1 
stop
}
}
