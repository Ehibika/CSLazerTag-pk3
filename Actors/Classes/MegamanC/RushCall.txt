actor Ability2_MegamanC : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "RushCall Ability"
weapon.ammotype2 "busterPower"
inventory.icon "RCOIL"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
HSPR B 0 
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",1)==1,"User1")
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",3)==1,"User3")
//==
"####" B 0 A_FireCustomMissile("RushCallRingSpawner",0,0,0,0)
"####" B 1 A_WeaponReady
goto ready

Fire:
"####" FFGGG 1
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("RushCallSpawner",0,0,0,0)
"####" B 1 A_TakeInventory("AbilityCharge_2",1)
"####" B 0 A_GiveInventory("AmmoStaller")
"####" A 0 ACS_NamedExecuteWithResult("CSLT_Chatline",CLT_ABILITY,Random(0,1))
"####" IJKLM 2
"####" B 0 A_Jump(256,"End")
goto End

AltFire:
"####" B 1
End:
"####" B 1 A_TakeInventory("AbilityActiveFlag",9)
"####" B 0 
Goto End
}
}
//====

actor UseAbility2_MegamanC : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("MegaRushCallAbility",1)
fail
}
}

actor MegaRushCallAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("AbilityCharge_2",1,"Pickup2")
stop

Pickup2:
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,2)
stop
}
}
//=================
//=================
Const int RushCallRad = 50;

actor RushCallRingGuide : BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		TNT1 A 0 A_JumpIf(!CallACS("Arc_ClientSide"),"Perish")
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("RushCallGuideDot", RushCallRad, 0, -(z-floorz)+5, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("RushCallGuideDot", -RushCallRad, 0, -(z-floorz)+5, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}


actor RushCallRingSpawner : FastProjectile
{
translation "198:198=220:220", "192:192=229:229"
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 600
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_SpawnItemEx("RushCallRingGuide",0,0,0,0,0,0,0,1)
stop
}
}

actor RushCallGuideDot : BasicGuideDot {}

actor RushCallSpawner : FastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 600
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)

Spawn2:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
WEAP B 0 A_SpawnItemEx("RushCallTeleport", 0, 0, 256, 0, 0, -32, 0, SXF_NOCHECKPOSITION, 0)
stop
}
}



actor RushCallTeleport
{
Radius 12
Height 32
scale 2.5
+NOINTERACTION
PROJECTILE
+DONTBLAST
+SKYEXPLODE
states
{
Spawn:
RUSH D 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
RUSH D 6
RUSH D 0 A_ChangeFlag("NOINTERACTION", 0)
RUSH DDDDDDDDDD 1 A_CheckFloor("Next")
Next:
RUSH D 0 //A_ChangeFlag("NOGRAVITY", 0)
Goto Death
Death:
TNT1 A 0 A_SpawnItemEx("RushCall",0,0,3,0,0,momz,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION,0)
stop
}
}

actor RushCall
{
- SOLID
+MISSILE
+DONTBLAST
+NOCLIP
+SKYEXPLODE
+LOOKALLAROUND
speed 0
scale 2.5
Radius 12
Height 32
reactiontime 250
meleerange 80
Args 50
states
{
Spawn:
RUSH D 0 A_Jump(256,"Death")
RUSH D 0 A_ChangeFlag("MISSILE",0)
TNT1 D 0 ACS_NamedExecuteAlways("GiveM_EntiTID",0,2,1)
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
RUSH D 0 A_ChangeVelocity(0, 0, -32, CVF_REPLACE)
RUSH D 1 A_JumpIf(z-floorz<=0, "Look")
Goto Spawn+4
Look:
RUSH E 0 A_ChangeFlag("NOCLIP",0)
RUSH EF 2
RUSH A 1 A_LookEX(LOF_NOSOUNDCHECK ,0,Args[0],0,0,"Look.Confirm")
RUSH A 0 A_CountDown
RUSH A 1 A_LookEX(LOF_NOSOUNDCHECK ,0,Args[0],0,0,"Look.Confirm")
RUSH A 0 A_CountDown
RUSH A 1 A_LookEX(LOF_NOSOUNDCHECK ,0,Args[0],0,0,"Look.Confirm")
RUSH A 0 A_CountDown

RUSH B 1 A_LookEX(LOF_NOSOUNDCHECK ,0,Args[0],0,0,"Look.Confirm")
RUSH B 0 A_CountDown
RUSH B 1 A_LookEX(LOF_NOSOUNDCHECK ,0,Args[0],0,0,"Look.Confirm")
RUSH B 0 A_CountDown
RUSH B 1 A_LookEX(LOF_NOSOUNDCHECK ,0,Args[0],0,0,"Look.Confirm")
RUSH B 0 A_CountDown
Goto Look+3

Look.Confirm:
RUSH "#" 1 A_JumpIfCloser(Args[0],"Melee")
Goto Look+3

Melee:
RUSH C 1 A_JumpIfInTargetInventory("StrongSpring",1,"Melee2")
RUSH C 0 A_PlaySoundEx("misc/boing","Voice")
RUSH C 18 A_GiveToTarget("SpringActivate",1)
RUSH C 0 A_Die
Goto Death
Melee2:
RUSH C 18 A_GiveToTarget("SpringActivate2",1)
RUSH C 0 A_PlaySoundEx("misc/boing","Voice")
RUSH C 0 A_Die
Goto Death
Death:
RUSH F 0 A_SpawnItemEx("RushDeath",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
stop
}
}