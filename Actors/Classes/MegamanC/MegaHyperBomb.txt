actor Ability3_MegamanC : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "HyperBomb Ability"
weapon.ammotype2 "busterPower"
inventory.icon "BOMBSI"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
Weapon.SlotPriority 5
States
{

Ready:
HSPR B 0 
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",1)==1,"User1")
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",2)==1,"User2")
//==
"####" B 0 A_FireCustomMissile("HyperBombAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" B 1 A_WeaponReady
goto ready

Fire:
"####" FFGGG 1
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("SubHyperBomb",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" B 1 A_TakeInventory("AbilityCharge_3",1)
"####" B 0 A_GiveInventory("AmmoStaller")
"####" A 0 ACS_NamedExecuteWithResult("CSLT_Chatline",CLT_ABILITY,Random(1,2))
"####" IJKLM 2
"####" B 0 A_Jump(256,"End")
goto End

AltFire:
"####" B 1
End:
"####" B 1 A_TakeInventory("AbilityActiveFlag",9)
"####" B 0 
Goto End
}
}
//====

actor UseAbility3_MegamanC : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("MegaHyperBombAbility",1)
fail
}
}

actor MegaHyperBombAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("AbilityCharge_3",1,"Pickup2")
stop

Pickup2:
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,3)
stop
}
}
//=================
//=================
actor HyperBombAimPath : GolfBallAimInstantStarter3 { Speed 35 Gravity 1.5}

actor SubHyperBomb 
{
PROJECTILE
damagetype "Explosion"
Obituary "$OB_Hyperbomb"
radius 4
height 4
scale 2.0
-NOGRAVITY
+DOOMBOuNCE
+BOUNCEONCEILINGS
+ALLOWBOUNCEONACTORS
+BOUNCEONACTORS
+MTHRUSPECIES
+USEBOUNCESTATE
+NOTARGETSWITCH
+NOEXPLODEFLOOR
+SLIDESONWALLS
+MBFBOUNCER
speed 35
gravity 1.5
Damage (0)
wallbouncefactor 0.8
BounceFactor 0.3
reactiontime 35
States
{
Spawn:
BOMB S 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
BOMB S 1 A_SpawnItemEX("S_HyperbombTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
BOMB S 0 A_CountDown
Goto Spawn2

Bounce.Floor:
Bounce.Ceiling:
MINB A 0 A_ChangeFlag("NOGRAVITY",false)
MINB A 0 A_ChangeFlag("USEBOUNCESTATE",false)
BOMB S 1 A_SpawnItemEX("SubHyperBomb2",0,0,0,momx,momy,momz,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM,0)
stop

Death:
MINB A 0 A_SpawnItemEx("S_HyperbombExplosion")
stop
Crash:
MINB A 0 A_SpawnItemEx("S_HyperbombExplosion")
stop
XDeath:
MINB A 0 A_SpawnItemEx("S_HyperbombFakeExplosion")
stop
}
}

	actor SubHyperBomb_BLUE : SubHyperBomb{}
	actor SubHyperBomb_RED : SubHyperBomb{translation "192:192=54:54", "198:198=42:42" , "196:196=170:170"}

actor SubHyperBomb2 : SubHyperBomb
{
-USEBOUNCESTATE
-NOGRAVITY
States
{
Spawn:
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
BOMB S 0 A_PlaySoundEX("weapon/Heat1","Soundslot6")
BOMB S 1 A_CountDown
BOMB S 0 A_CheckFloor("SlowDown")
loop
SlowDown:
BOMB S 0 A_ScaleVelocity(0.3)
Goto Spawn2
	
Death:
MINB A 0 A_SpawnItemEx("S_HyperbombExplosion")
stop
Crash:
MINB A 0 A_SpawnItemEx("S_HyperbombExplosion")
stop
XDeath:
MINB A 0 A_SpawnItemEx("S_HyperbombFakeExplosion")
stop
}
}

actor S_HyperbombExplosion : BasicExplosion
{
Scale 5.5
States
{
Spawn:
NAPC A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
NAPC D 0 A_Explode(50, 216, 1,0,108)
NAPC D 0 A_Explode(50, 216, 1,0,216)
NAPC A 0 A_PlaySoundEx("weapon/HyperBomb", "Weapon")
MXP2 ABCDEFGHIJKLMNOPQRST 1 
stop
}
}

actor S_HyperbombFakeExplosion : BasicExplosion
{
Scale 5.5
States
{
Spawn:
NAPC A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
NAPC A 0 A_PlaySoundEx("weapon/HyperBomb", "Weapon")
MXP2 ABCDEFGHIJKLMNOPQRST 1
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Effect Actors|------------------------------
//[+]========================================================================[+]
actor S_HyperbombTrail : BasicClientside
{
scale 1.5
States
{
Spawn:
TNT1 A 0
TNT1 A 2
MINB IIIIII 1
MINB IIIIII 1 A_Fadeout
stop
}
}