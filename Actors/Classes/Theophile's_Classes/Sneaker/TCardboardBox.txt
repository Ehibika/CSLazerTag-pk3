actor Ability1_TSneaker : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "Cardboard Box Ability"
weapon.ammotype2 "busterPower"
inventory.icon "CARBOX2"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
HSPR B 0 
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",2)==1,"User2")
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",3)==1,"User3")
//==
"####" B 0 A_FireCustomMissile("CBoxRingSpawner",0,0,0,0)
"####" B 1 A_WeaponReady
goto ready

Fire:
"####" FFGGG 1
"####" C 0 A_PlaySoundEx("weapon/WaterBalloon","Weapon")
"####" B 0 A_FireCustomMissile("CBoxSpawner",0,0,0,0)
"####" B 1 A_TakeInventory("AbilityCharge_1",1)
"####" B 0 A_GiveInventory("AmmoStaller")
"####" B 0 A_GiveInventory("SignatureRecharge_20") //
"####" IJKLM 2
"####" B 0 A_Jump(256,"End")
goto End

AltFire:
"####" B 1
End:
"####" B 1 A_TakeInventory("AbilityActiveFlag",9)
"####" B 0 
Goto End
}
}
//====

actor UseAbility1_TSneaker : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("TSneakerCardboardBoxAbility",1)
fail
}
}

actor TSneakerCardboardBoxAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("AbilityCharge_1",1,"Pickup2")
stop

Pickup2:
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,1)
stop
}
}
//=================
//=================
Const int CBoxRad = 75;

actor CBoxRingGuide : BasicHelper
{
translation "198:198=220:220", "192:192=229:229"
	reactiontime 15
	var int user_angle;
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
		TNT1 A 0 A_JumpIf(!CallACS("Arc_ClientSide"),"Perish")
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("CBoxGuideDot", CBoxRad, 0, -(z-floorz)+5, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SpawnItemEx("CBoxGuideDot", -CBoxRad, 0, -(z-floorz)+5, 0, 0, 0, user_angle, 1061)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0
		stop
	}
}


actor CBoxRingSpawner : FastProjectile
{
translation "198:198=220:220", "192:192=229:229"
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 250
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_SpawnItemEx("CBoxRingGuide",0,0,0,0,0,0,0,1)
stop
}
}

actor CBoxGuideDot : BasicGuideDot {}

actor CBoxSpawner : FastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 250
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)

Spawn2:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
WEAP B 0 A_SpawnItemEx("CardboardBoxDummyJ", 0, 0, -(z-floorz), 0, 0, 0, 0, 1, 0)
stop
}
}

actor CardboardBoxDummyJ
{
PROJECTILE
+NOCLIP
-NOGRAVITY
-MISSILE
Radius 16
Height 56
scale 2.5
speed 0
RenderStyle "Translucent"
Alpha 0.5
+MOVEWITHSECTOR
+DONTREFLECT
+DONTBLAST
states
{
Spawn:
CARB A 0
CARB A 0 ACS_NamedExecuteAlways("GiveM_EntiTID",0,5,3)
Spawn2:
CARB A 1 A_RadiusGive("CardBoardBoxRadiusFlag",CBoxRad,RGF_PLAYERS,1)
loop
Death:
TNT1 A 0
stop
}
}

actor CBoxSlowPower : PowerUp
{
powerup.duration 7
}


actor CBoxSlowGiver
{
+MISSILE
height 1
radius 1
renderstyle none
+NOGRAVITY
-SOLID
+NOINTERACTION
states
{
Spawn:
TNT1 A 0
Spawn2:
TNT1 A 1 A_GiveToTarget("CBoxSlow",1)
TNT1 A 0 A_JumpIfInTargetInventory("CBoxSlowPower",1,"Spawn2")
stop
}
}

actor CBoxSlow : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_ChangeVelocity(momx*3/4,momy*3/4,momz,CVF_REPLACE)
stop
}
}

actor CardboardBoxRadiusFlag : PowerInvisibility
{
+CANTSEEK
+DONTSPLASH
Powerup.Strength 100
Powerup.Duration -22
powerup.duration 7
}

actor SpawnDMBoxRemove : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIf(CallACS("LMS_CHECK"),"Permanent")
TNT1 A 0 A_SpawnItemEx("DMBoxRemover")
//TNT1 A 0 A_SpawnItemEx("DMBoxRemover",0,0,0,0,0,0,0,SXF_SETMASTER)
stop
Permanent:
TNT1 A 0
stop
}
}

actor DMBoxRemover
{
Height 0
Radius 0
+NOINTERACTION
+NOCLIP
PROJECTILE
//reactiontime 70
reactiontime 2100
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_RearrangePointers(AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_DEFAULT,0) //master is target
Spawn1:
TNT1 A 1 A_CountDown
loop
Death:
//TNT1 A 1 A_RemoveTarget
TNT1 A 1 A_RemoveMaster
stop
}
}