actor Ability3_TGrand : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "%o got a new hole because of %k's Bullet Grenade."
Tag "Bullet Grenade Ability"
weapon.ammotype2 "busterPower"
inventory.icon "BALLSI"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
0G_H A 0 
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",1)==1,"User1")
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",2)==1,"User2")
"####" A 0 A_FireCustomMissile("BulletGrenadeAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" A 1 A_WeaponReady
goto ready

Select:
//TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
0G_H AAA 0 A_Raise
0G_H A 1 
Loop

Fire:
"####" C 0 A_PlaySoundEx("weapon/AutoFirework","Weapon")
"####" B 0 A_FireCustomMissile("SubBulletGrenade",0,1,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" B 0 A_TakeInventory("AbilityCharge_3",1)
"####" B 0 A_GiveInventory("AmmoStaller")
"####" B 2 Offset(4,28)
"####" C 2 Offset(8,24)
"####" B 2 Offset(4,28)
"####" A 5 A_WeaponReady(14)
"####" D 5 A_PlaySound("weapon/AutoReload1",4,1.0)
"####" D 0 A_PlaySound("weapon/AutoReload2",1,0.75)
"####" D 2 Offset(-6,44)
"####" D 2 Offset(-6,44)
"####" D 6 A_WeaponReady(14)
"####" A 8 A_PlaySound("weapon/AutoReload3",1,0.75)
"####" A 1 A_JumpIfInventory("AbilityCharge_3",1,"Ready")
"####" B 0 A_Jump(256,"End")
goto End

AltFire:
"####" A 1
End:
"####" A 1 A_TakeInventory("AbilityActiveFlag",9)
"####" A 0 
Goto End
}
}
//====

actor UseAbility3_TGrand : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("BulletGrenadeAbility",1)
fail
}
}

actor BulletGrenadeAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("AbilityCharge_3",1,"Pickup2")
stop

Pickup2:
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,3)
stop
}
}
//=================
//=================

actor BulletGrenadeAimPath : GolfBallAimInstantStarter3 { Speed 60 Gravity 1.5}

actor SubBulletGrenade : SubHyperBomb
{
Damagetype "BulletGrenade"
Obituary "%o got a new hole because of %k's Bullet Grenade."
radius 4
height 4
scale 2.0
speed 60
gravity 1.5
Damage (0)
wallbouncefactor 0.5
BounceFactor 0.5
reactiontime 35
States
{
Spawn:
BOMB S 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
BALA ABCD 1 A_SpawnItemEX("S_BulletGrenadeTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
BOMB S 0 A_CountDown
Goto Spawn2

Bounce.Floor:
Bounce.Ceiling:
MINB A 0 A_ChangeFlag("NOGRAVITY",false)
MINB A 0 A_ChangeFlag("USEBOUNCESTATE",false)
MINB A 0 A_SetPitch(reactiontime)
BOMB S 1 A_SpawnItemEX("SubBulletGrenade2",0,0,0,momx,momy,momz,0,SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH|SXF_ABSOLUTEMOMENTUM,0)
stop

Death:
MINB A 0 A_SpawnItemEx("S_BulletGrenadeExplosion")
stop
}
}

actor SubBulletGrenade2 : SubBulletGrenade
{
-USEBOUNCESTATE
-NOGRAVITY
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SetArg(0,Pitch)
Spawn2:
BALA A 0 A_PlaySoundEX("weapon/Heat1","Soundslot6")
BALA A 1 A_CountDownArg(0,"Death")
BALA A 0 A_CheckFloor("SlowDown")
BALA A 0 A_SpawnItemEX("S_GrandBombTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
BALA B 1 A_CountDownArg(0,"Death")
BALA A 0 A_SpawnItemEX("S_GrandBombTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
BALA B 0 A_CheckFloor("SlowDown")
BALA C 1 A_CountDownArg(0,"Death")
BALA A 0 A_SpawnItemEX("S_GrandBombTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
BALA C 0 A_CheckFloor("SlowDown")
BALA D 1 A_CountDownArg(0,"Death")
BALA A 0 A_SpawnItemEX("S_GrandBombTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
BALA D 0 A_CheckFloor("SlowDown")
loop
SlowDown:
BALA A 1 A_ScaleVelocity(0.3)
Goto Spawn2
	
Death:
MINB A 0 A_SpawnItemEx("S_BulletGrenadeExplosion")
MINB A 0 A_SpawnItemEx("BulletGrenadeExplosionFX")
stop
}
}

actor S_BulletGrenadeExplosion : BasicExplosion
{
Obituary "%o got dozens of new holes because of %k's Bullet Grenade."
Scale 5.0
Reactiontime 17
States
{
Spawn:
NAPC A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
NAPC D 0 A_Explode(50, 194, 1,0,97) //108*0.9=97.2
NAPC D 0 A_Explode(50, 194, 1,0,194) //216*0.9=194.4
NAPC A 0 A_PlaySoundEx("weapon/GrandBomb", "Weapon")
//NAPC Ax30 0
Spawn2:
TNT1 AA 0 A_SpawnItemEx("BulletGrenade_Beam",0,0,16,60,random(0,60),random(0,60),random(0,360),0,0,1)
TNT1 A 1 A_SpawnItemEx("BulletGrenade_Beam",0,0,16,60,0,0,random(0,360),0,0,1)
NAPC A 0 A_CountDown
Loop
}
}

actor BulletGrenadeExplosionFX : BasicExplosion
{
Scale 5.5
States
{
Spawn:
NAPC A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
NAPC A 0 A_PlaySoundEx("weapon/HyperBomb", "Weapon")
MXP2 ABCDEFGHIJKLMNOPQRST 1
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Effect Actors|------------------------------
//[+]========================================================================[+]
actor S_BulletGrenadeTrail : BasicClientside
{
scale 1.5
States
{
Spawn:
TNT1 A 0
TNT1 A 2
BALA ABCDAB 1
BALA CDABCD 1 A_Fadeout
stop
}
}

//Beam stuff, this is why it's called "Bullet Grenade", huh.

Const int BulletGrenade_SPD = 1.8;
Const int BulletGrenade_BloomRate = 12;

Const int BulletGrenade_Damage = 15; //30
Const int BulletGrenade_FalloffRange = 600;
Const int BulletGrenade_FalloffRate = 20;

Const int BulletGrenade_HeadShotDamage = 30; //45
Const int BulletGrenade_HeadShotFalloffRange = 600;
Const int BulletGrenade_HeadShotFalloffRate = 50;


actor BulletGrenade_Beam : LazerTagBeam
{
ARGS 5
States
{

Spawn.Beam:
TNT1 A 0 A_PlaySoundEx("weapon/Gemini","Weapon")
TNT1 A 0 A_SpawnItemEx("Muscler_BeamFX2",0,0,0,momx,momy,momz,0,9)
TNT1 A 0 A_Jump(256,"Spawn.Hitbox")
goto Spawn.Hitbox

}
}

actor BulletGrenade_BeamHitBox : BasicFastProjectile
{
+SERVERSIDEONLY
+HITTRACER
reactiontime 1
states
{
Spawn:
PLAY A 0
Spawn2:
PLAY A 1 A_CountDown
loop
XDeath:
TNT1 A 0 A_RemoveSiblings(true)
AQBA L 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("TracerHealthGet")>0,"Headshotcheck")
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
HeadShotCheck:
TNT1 A 0 A_JumpIf(Checkclass("PlayerPawn",AAPTR_TRACER,true)&&z>=ACS_NamedExecuteWithResult("TF2_TracerZGet")+40,"DoHeadshot")
TNT1 A 0 A_SpawnItemEx("BulletGrenade_BodyShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
DoHeadShot:
TNT1 A 0 A_SpawnItemEx("BulletGrenade_HeadShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("PowerStrikeFX",0,0,0,0,0,0,0,1)
stop
Death:
TNT1 A 0 A_Jumpif(reactiontime <= 0,"Terminate")
TNT1 A 0 A_GiveInventory("BurrowDecayFunc",1,AAPTR_MASTER)
stop

Terminate:
TNT1 A 0
stop
}
}


actor BulletGrenade_BodyShotter : TF2BodyShotter
{
Obituary "%o got a new hole because of %k's Bullet Grenade. (Bodyshotted)"
args 30
states
{

Spawn2:
TNT1 A 0 A_Explode(CallACS("DamageRate",BulletGrenade_Damage,BulletGrenade_FalloffRange,BulletGrenade_FalloffRate),1,0,0,1)
TNT1 A 1
stop
}
}

actor BulletGrenade_WallBodyShotter : BulletGrenade_BodyShotter
{Obituary "%o got a new hole because of %k's Bullet Grenade. (WallBanged)"}

actor BulletGrenade_HeadShotter : TF2HeadShotter
{
Obituary "%o got a new hole because of %k's Bullet Grenade. (Headshotted)"
args 45
states
{
Spawn2:
TNT1 A 0 A_Explode(CallACS("DamageRate",BulletGrenade_HeadShotDamage,BulletGrenade_HeadShotFalloffRange,BulletGrenade_HeadShotFalloffRate),1,0,0,1)
TNT1 A 1
stop
}
}

actor BulletGrenade_WallHeadShotter : BulletGrenade_HeadShotter
{Obituary "%o got a new hole because of %k's Bullet Grenade. (Wallbanged Headshotted)"}