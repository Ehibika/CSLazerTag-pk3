actor Ability1_TAssassin : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "Weaken Shuriken Ability"
weapon.ammotype2 "busterPower"
inventory.icon "SHADI"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
HSPR A 0 
"####" A 0
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",2)==1,"User2")
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",3)==1,"User3")
"####" A 1 A_WeaponReady
goto ready

Fire:
"####" KJI 1
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("WeakenShuriken_Beam",-3,0,1,0)
"####" B 0 A_FireCustomMissile("WeakenShuriken_Beam",0,0,1,0)
"####" B 0 A_FireCustomMissile("WeakenShuriken_Beam",3,0,1,0)
"####" B 1 A_TakeInventory("AbilityCharge_1",1)
"####" B 0 A_GiveInventory("AmmoStaller")
"####" B 0 A_GiveInventory("SignatureRecharge_15")
"####" HGFFFF 1
"####" B 0 A_Jump(256,"EndM")
goto End

AltFire:
"####" F 1
End:
"####" F 1 A_TakeInventory("AbilityActiveFlag",9)
"####" F 0 
Goto End
EndM: //Mainfire, mainfire.
"####" F 1 A_TakeInventory("AbilityActiveFlag",9)
"####" F 0 
Goto End
}
}
//====

actor UseAbility1_TAssassin : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("TAssassinWeakenShurikenAbility",1)
fail
}
}

actor TAssassinWeakenShurikenAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("AbilityCharge_1",1,"Pickup2")
stop

Pickup2:
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,1)
stop
}
}
//=================
//=================

Const int WeakenShuriken_SPD = 1.8;
Const int WeakenShuriken_BloomRate = 12;

Const int WeakenShuriken_Damage = 10; //30
Const int WeakenShuriken_FalloffRange = 600;
Const int WeakenShuriken_FalloffRate = 20;

Const int WeakenShuriken_HeadShotDamage = 25; //45
Const int WeakenShuriken_HeadShotFalloffRange = 600;
Const int WeakenShuriken_HeadShotFalloffRate = 50;

actor WeakenShuriken_Beam : BasicFastProjectile
{
Reactiontime 70
+HITTRACER
Speed 160
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
TNT1 A 0 A_PlaySoundEx("weapon/Gemini","Weapon")
TNT1 A 0 A_SpawnItemEx("WSLazerTagBeamFX1",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("WSLazerTagBeamFX2",0,0,0,momx,momy,momz,0,9)
TNT1 A 1 A_CountDown
loop
Death:
AQBA L 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("TracerHealthGet")>0,"Headshotcheck")
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
HeadShotCheck:
TNT1 A 0 A_JumpIf(Checkclass("PlayerPawn",AAPTR_TRACER,true)&&z>=ACS_NamedExecuteWithResult("TF2_TracerZGet")+40,"DoHeadshot")
TNT1 A 0 A_SpawnItemEx("WeakenShuriken_BodyShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("LazerTagPuff",0,0,0,0,0,0,0,1)
stop
DoHeadShot:
TNT1 A 0 A_SpawnItemEx("WeakenShuriken_HeadShotter",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("PowerStrikeFX",0,0,0,0,0,0,0,1)
stop
}
}

actor WSLazerTagBeamFX1 : BasicClientSide
{
+FORCEXYBILLBOARD
Renderstyle "Add"
Scale 2.5
States
{
Spawn:
AQBA L 0 A_GiveInventory("ProjectileCode",1)
SBLA AB 1 
stop
}
}

actor WSLazerTagBeamFX2 : BasicExplosion
{
Obituary "$OB_MegaBuster"
damagetype "MegaBuster"
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("WSLazerTagBeamFX3",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("WSLazerTagBeamFX3",0,0,0,-momx*0.1,-momy*0.1,-momz*0.1,0,9)
TNT1 A 0 A_SpawnItemEx("WSLazerTagBeamFX3",0,0,0,-momx*0.2,-momy*0.2,-momz*0.2,0,9)
TNT1 A 0 A_SpawnItemEx("WSLazerTagBeamFX3",0,0,0,-momx*0.3,-momy*0.3,-momz*0.3,0,9)
TNT1 A 0 A_SpawnItemEx("WSLazerTagBeamFX3",0,0,0,-momx*0.4,-momy*0.4,-momz*0.4,0,9)
TNT1 A 0 A_SpawnItemEx("WSLazerTagBeamFX3",0,0,0,-momx*0.5,-momy*0.5,-momz*0.5,0,9)
TNT1 A 0 A_SpawnItemEx("WSLazerTagBeamFX3",0,0,0,-momx*0.6,-momy*0.6,-momz*0.6,0,9)
TNT1 A 0 A_SpawnItemEx("WSLazerTagBeamFX3",0,0,0,-momx*0.7,-momy*0.7,-momz*0.7,0,9)
TNT1 A 0 A_SpawnItemEx("WSLazerTagBeamFX3",0,0,0,-momx*0.8,-momy*0.8,-momz*0.8,0,9)
TNT1 A 0 A_SpawnItemEx("WSLazerTagBeamFX3",0,0,0,-momx*0.9,-momy*0.9,-momz*0.9,0,9)
TNT1 A 1 
stop
}
}

actor WSLazerTagBeamFX3 : BasicClientSide
{
+FORCEXYBILLBOARD
Renderstyle "Add"
Scale 2.5
States
{
Spawn:
ICEL I 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
ICEL I 0 A_Stop
SBLA AB 1
SBLA AB 1 A_Fadeout(0.5)
SBLA AB 1 A_Fadeout(0.25)
SBLA AB 1
//ICEL IZIZ 2
stop
}
}


actor WeakenShuriken_BodyShotter : TF2BodyShotter
{
damagetype "WeakenShuriken"
args 30
states
{

Spawn2:
TNT1 A 0 A_Explode(CallACS("DamageRate",WeakenShuriken_Damage,WeakenShuriken_FalloffRange,WeakenShuriken_FalloffRate),1,0,0,1)
TNT1 A 1
stop
}
}


actor WeakenShuriken_HeadShotter : TF2HeadShotter
{
damagetype "WeakenShuriken"
args 45
states
{
Spawn2:
TNT1 A 0 A_Explode(CallACS("DamageRate",WeakenShuriken_HeadShotDamage,WeakenShuriken_HeadShotFalloffRange,WeakenShuriken_HeadShotFalloffRate),1,0,0,1)
TNT1 A 1
stop
}
}


//Guess what this comes from :)
actor CursedGlobalDoubleLowDamage : PowerupGiver
{
powerup.type "CursedGlobalDoubleLowDamage"
+INVENTORY.AUTOACTIVATE
inventory.maxamount 0
powerup.duration -5
states
{
Spawn:
AUSH A 1
loop
}
}

actor WeakenFade : Powerup
{
powerup.duration 0x7FFFFFFD
Powerup.Color 248 58 0, 0.4
}

actor PowerCursedGlobalDoubleLowDamage : PowerDamage
{
Powerup.Duration -5
DamageFactor "Normal", 0.5
}

actor ShurikenWeakenEffect : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInventory("PowerCursedGlobalDoubleLowDamage", 1, "Pickup2")
TNT1 A 0 A_SpawnItemEx("WeakenWatcher")
TNT1 A 0 A_PlaySoundEx("Support/Negative","Weapon")
goto Pickup2

Pickup2:
TNT1 A 0 A_GiveInventory("PowerCursedGlobalDoubleLowDamage", 1)
TNT1 A 0 A_GiveInventory("WeakenFade", 1)
stop
}
}

actor WeakenWatcher
{
Height 0
Radius 0
+NOINTERACTION
-SOLID
+NOGRAVITY
+MISSILE
States
{
Spawn:
TNT1 A 0 
TNT1 A 3 A_GiveToTarget("WeakenFXSpawn", 1)
TNT1 A 0 A_JumpIfInTargetInventory("PowerCursedGlobalDoubleLowDamage", 1, "Spawn")
TNT1 A 0 A_TakeFromTarget("WeakenFade", 1)
stop
}
}

actor WeakenFXSpawn : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_SpawnItemEx("WeakenFX",30,0,80,0,0,-3,random(0,360))
stop
Nope:
TNT1 A 0
stop
}
}

actor WeakenFX 
{
PROJECTILE
+NOINTERACTION
+DONTBLAST
+CLIENTSIDEONLY
Translation "192:192=171:171", "198:198=41:41"
Speed 0
radius 5
height 5
scale 2.0
Renderstyle "Translucent"
Alpha 0.8
States
{
Spawn:
ICMS A 0
POWU CDCD 2
POWU CDCD 2 A_Fadeout
stop
Death:
TNT1 A 1
stop
}
}