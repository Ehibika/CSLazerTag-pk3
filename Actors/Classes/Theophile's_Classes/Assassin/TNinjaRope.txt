actor Ability2_TAssassin : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "Ninja Rope Ability"
weapon.ammotype2 "busterPower"
inventory.icon "NROPSI"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
HSPR B 0 
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",1)==1,"User1")
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",3)==1,"User3")
"####" B 1 A_WeaponReady
goto ready

Fire:
"####" B 0 A_TakeInventory("AbilityCharge_2",1)
"####" CCDD 1
"####" D 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" D 0 A_FireCustomMissile("NinjaRopeKunai",0,0,0,0)
Fire.HookShot:
"####" D 2 
"####" D 1 A_JumpIfInventory("WireHookShotDelay",1,"Fire.HookShot")

Fire.HookShotEnd:
"####" D 0 A_GiveInventory("AmmoStaller")
"####" DELM 2
"####" M 0 A_Jump(256,"End")
goto End

AltFire:
"####" M 1
End:
"####" M 1 A_TakeInventory("AbilityActiveFlag",9)
"####" M 0 
Goto End
}
}
//====

actor UseAbility2_TAssassin : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("MegaNinjaRopeAbility",1)
fail
}
}

actor MegaNinjaRopeAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("AbilityCharge_2",1,"Pickup2")
stop

Pickup2:
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,2)
stop
}
}
//=================
//=================
actor NinjaRopeKunai : WireAdaptorHook
{
Translation "199:199=193:193"
ReactionTime 3
damagetype "NinjaRope"
Obituary "%o made top 10 saddest anime deaths from %k's ninja rope."
+RIPPER
Damage (18)
+SKYEXPLODE
states
{
Spawn:
NROP B 0
NROP B 0 A_SetUserVar("user_CountDown",reactiontime)
NROP B 0 A_SetUserVar(user_PTID, ACS_NamedExecuteWithResult("core_gettarget"))
NROP B 0 A_GiveInventory("WeaponCharge",1)
//NROP B 0 A_ScaleVelocity(44)//Speed of projectile.
NROP B 0 A_ScaleVelocity(60)
NROP B 0 A_Jump(256,"Spawn2")
goto Spawn2

Spawn2:
NROP B 0 ACS_NamedExecuteAlways("core_drawhookshot ", 0, user_PTID, 18)
NROP B 0 A_GiveToTarget("WireHookShotDelay",1)
NROP BBBBBB 1 A_JumpIf(ACS_NamedExecuteWithResult("core_targetexists")==0,"SuperDeath")
NROP B 0 A_GiveToTarget("WireHookShotDelay",1)
NROP BBBBBB 1 A_JumpIf(ACS_NamedExecuteWithResult("core_targetexists")==0,"SuperDeath")
NROP B 0 A_Jump(256,"WireLoop")
WireLoop:
NROP B 0 A_GiveToTarget("WireHookShotDelay",1)
NROP BBBBBB 1 A_JumpIf(ACS_NamedExecuteWithResult("core_targetexists")==0,"SuperDeath")
NROP B 0 A_SetUserVar("user_CountDown",user_CountDown - 1)
NROP B 0 A_CountDown
loop

Death:
NROP B 0 A_JumpIfCloser(64, "SuperDeath")
XDeath:
Crash:
NROP B 0 A_JumpIf(user_CountDown == 0, "SuperDeath")
NROP B 0 A_JumpIfInventory("WeaponCharge",2,"SuperDeath")
NROP B 0 A_JumpIfInventory("WeaponCharge",1,1)
goto SuperDeathPoof//Kill hook early if it never got through the Spawn state.
NROP B 0 A_PlaySoundEx("weapon/crashland","Weapon")
NROP B 0 A_JumpIfInventory("NoHookshot",1,"Death1",AAPTR_TRACER)
NROP B 0 A_CheckFlag("SHOOTABLE","Death2",AAPTR_TRACER)
NROP B 0 A_Jump(256,"Death1")
goto Death1
Death1://Tracer isn't shootable (probably can't move)
NROP B 0 ACS_NamedExecuteAlways("core_hookshot",0,user_PTID,30,0)
NROP B 3
NROP B 0 A_Jump(256,"DeathLoop1")
goto DeathLoop1
Death2://Tracer IS shootable (probably CAN move)
NROP B 0 ACS_NamedExecuteAlways("core_hookshot",0,user_PTID,30,1)
NROP B 3//Needs at least 3 tics of delay.
NROP B 0 A_Jump(256,"DeathLoop2")
goto DeathLoop2
DeathLoop1:
NROP B 0 A_JumpIfCloser(64,"SuperDeathPoof")
NROP B 0 A_GiveToTarget("WireHookShotDelay",1)
NROP B 1 A_JumpIf(ACS_NamedExecuteWithResult("core_targetexists")==0,"SuperDeath")
loop
DeathLoop2:
NROP B 0 A_JumpIfCloser(64,"SuperDeathPoof")
NROP B 0 A_Warp(AAPTR_TRACER,-4,0,28)
NROP B 0 A_GiveToTarget("WireHookShotDelay",1)
NROP B 0 A_JumpIf(ACS_NamedExecuteWithResult("core_targetexists")==0,"SuperDeath")
NROP B 1 A_JumpIf(ACS_NamedExecuteWithResult("core_targetexists",1)==0,"DeathLoop1")
loop
SuperDeathPoof:
NROP B 0 A_PlaySoundEx("weapon/adapterreturn","Weapon")
NROP B 0 A_TakeFromTarget("WireAdaptorDelay",1)
NROP B 0 A_ChangeFlag("NOINTERACTION",1)
NROP B 6 A_Stop
stop
SuperDeath:
NROP B 0
stop
}
}

actor NinjaRopeKunaiFX : WireAdaptorHookFX
{
States
{
Spawn:
NROP C 0
NROP C 1 
stop
}
}