
actor Ability3_Cinder : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 2
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "SmokeScreen Gun"
//weapon.ammotype2 "SmokeScreenAmmo"
inventory.icon "BUSTI"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
HSPR A 0 
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",1)==1,"User1")
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",2)==1,"User2")
//==
"####" A 0 A_FireCustomMissile("FireFieldAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" A 1 A_WeaponReady
goto ready

Fire:
"####" FFGGG 1
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("FireFieldShot",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" B 1 A_TakeInventory("AbilityCharge_3",1)
"####" B 0 A_GiveInventory("AmmoStaller")
"####" IJKLM 2
"####" B 0 A_Jump(256,"End")
goto End

AltFire:
"####" B 1
End:
"####" B 1 A_TakeInventory("AbilityActiveFlag",9)
"####" B 0 
Goto End
}
}
//====

actor UseAbility3_Cinder : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("FireFieldAbility",1)
fail
}
}

actor FireFieldAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("AbilityCharge_3",1,"Pickup2")
stop

Pickup2:
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,3)
stop
}
}
//=================
//=================

Const int FireFieldArea = 150;

actor FireFieldAimPath : GolfBallAimInstantStarter3 { Speed 35 Gravity 1.5}

actor FireFieldShot
{
PROJECTILE
radius 4
height 4
scale 2.0
-NOGRAVITY
+DOOMBOuNCE
+BOUNCEONCEILINGS
+ALLOWBOUNCEONACTORS
+BOUNCEONACTORS
+MTHRUSPECIES
+USEBOUNCESTATE
+NOTARGETSWITCH
+NOEXPLODEFLOOR
+SLIDESONWALLS
+MBFBOUNCER
speed 35
gravity 1.0
RenderStyle "Add"
Damage (0)
wallbouncefactor 1.0
BounceFactor 0.1
reactiontime 1
States
{
Spawn:
FIRE D 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
FIRE D 1 A_SpawnItemEX("FireFieldTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
Goto Spawn2

Bounce.Floor:
Bounce.Ceiling:
FIRE D 0 A_ChangeFlag("USEBOUNCESTATE",false)
FIRE D 1 A_SpawnItemEX("FireFieldShot2",0,0,0,momx,momy,momz,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM,0)
stop

Death:
FIRE D 0 A_SpawnItemEx("FireFieldMolotov")
HARD BCDE 2
stop
Crash:
FIRE D 0 A_SpawnItemEx("FireFieldMolotov")
HARD BCDE 2
stop
XDeath:
HARD BCDE 2
stop
}
}

actor FireFieldShot2 : FireFieldShot
{
-USEBOUNCESTATE
-NOGRAVITY
States
{
Spawn:
FIRE D 0 A_PlaySoundEX("weapon/Heat1","Soundslot6")
FIRE D 0 A_CheckFloor("Death")
FIRE D 1 A_CountDown
loop
SlowDown:
FIRE D 0 A_ScaleVelocity(0.1)
FIRE D 1 A_CountDown
Goto Spawn
	
Death:
FIRE D 0 A_SpawnItemEx("FireFieldMolotov")
HARD BCDE 2
stop
Crash:
FIRE D 0 A_SpawnItemEx("FireFieldMolotov")
HARD BCDE 2
stop
XDeath:
HARD BCDE 2
stop
}
}


actor FireFieldMolotov : BasicExplosion2
{
DamageType "FireField"
obituary "$OB_FIREFIELD"
+OLDRADIUSDMG
ReactionTime 17
Args 0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCodeSyncTarget",1)
//TNT1 A 0 ACS_NamedExecuteAlways("GiveEntiTID",0,ToxEntiTID)
TNT1 A 5
TNT1 A 0 A_Explode(5,FireFieldArea,1,0,FireFieldArea)
Spawn2:
TNT1 A 1
TNT1 A 0 A_PlaySoundEx("weapon/WaveBurner","Weapon")
TNT1 AAAAAA 0 A_SpawnItemEx("FireSpiritParticleSpawner",Random(-FireFieldArea,FireFieldArea),0,-(z-floorz)+random(0,5),0,0,6,Random(1,360),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SetArg(0,Args[0]+1)
TNT1 A 0 A_JumpIf(Args[0] >= 7,"Spawn3")
Goto Spawn2
Spawn3:
TNT1 A 0 A_RadiusGive("FireFieldOffGround",FireFieldArea,RGF_PLAYERS|RGF_MONSTERS|RGF_OBJECTS)
TNT1 A 0 A_Explode(5,FireFieldArea,1,0,FireFieldArea)
TNT1 A 0 A_SetArg(0,0)
TNT1 A 0 A_CountDown
Goto Spawn2
Death:
TNT1 A 0 
stop
}
}

actor FireFieldOffGround : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_CheckFloor("Nope")
TNT1 A 0 A_GiveInventory("FireFieldNoGround",1)
stop

Nope:
TNT1 A 0
stop
}
}

actor FireFieldNoGround : PowerProtection
{
Powerup.Duration 2
damagefactor "FireField", 0.0
}

actor FireFieldTrail : BasicClientside
{
scale 1.5
RenderStyle "Add"
States
{
Spawn:
TNT1 A 0
FIRE DDEEFF 1
FIRE FFF 1 A_Fadeout
stop
}
}