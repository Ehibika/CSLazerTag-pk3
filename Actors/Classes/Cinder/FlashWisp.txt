actor Ability1_Cinder : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 2
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "SmokeScreen Gun"
weapon.ammotype2 "SmokeScreenAmmo"
inventory.icon "BUSTI"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
HSPR B 0 
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",2)==1,"User2")
"####" "#" 0 A_Jumpif(CallACS("CSLT_CheckAbilityInput",3)==1,"User3")
//==
"####" A 1 A_WeaponReady
goto ready

Fire:
"####" B 1
"####" C 0 A_PlaySoundEx("weapon/FireStorm","Weapon")
"####" B 0 A_SpawnItemEx("FlashWispShot", 0, 8, 32, cos(-pitch)*10, 0, sin(-pitch)*10, 0, SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH)
"####" B 1 A_TakeInventory("AbilityCharge_1",1)
"####" B 0 A_GiveInventory("FlashWispFlag",1)
"####" B 0 A_GiveInventory("FlashWispActiveFlag")
"####" B 0 A_GiveInventory("AmmoStaller")
"####" B 0 A_GiveInventory("SignatureRecharge") //
"####" B 2
"####" B 0 A_Jump(256,"Fire.Channel1")
goto Fire.Channel1

Fire.Channel1:
"####" B 1
"####" B 0 A_Jumpif(CallACS("CSLT_ButtonCheck",BT_ATTACK)==1,"fire.Channel1")
"####" B 0 A_Jumpif(CallACS("CSLT_ButtonCheck",BT_ALTATTACK)==1,"fire.Channel1")
goto Fire.Channel2
Fire.Channel2:
"####" B 0 A_Jumpif(CallACS("CSLT_ButtonCheck",BT_ATTACK)==1,"fire.Trigger")
"####" B 0 A_Jumpif(CallACS("CSLT_ButtonCheck",BT_ALTATTACK)==1,"fire.Trigger")
"####" B 1
"####" B 1 A_JumpIfInventory("FlashWispActiveFlag",1,"Fire.Channel2")
"####" B 0 A_Jump(256,"End")
goto End

Fire.Trigger:
"####" B 1 A_GiveInventory("FlashWispFlag",1)
"####" CDDEE 2
"####" B 0 A_TakeInventory("FlashWispFlag",999)
"####" B 0 A_Jump(256,"End")
goto End

AltFire:
"####" B 1
End:
"####" B 1 A_TakeInventory("AbilityActiveFlag",9)
"####" B 0 
Goto End
}
}
//====

actor UseAbility1_Cinder : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("FlashWispAbility",1)
fail
}
}

actor FlashWispAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("AbilityCharge_1",1,"Pickup2")
stop

Pickup2:
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,1)
stop
}
}
//=================
//=================

//Const int FlashWispArea = 150;
//Const int FlashWispArea2 = 160;
Const int RGI_FlashWisp = 2;

actor FlashWispFlag : BlackHoleFlag{}
actor FlashWispActiveFlag : Powerup {inventory.amount 1 powerup.duration 17 }

actor FlashWispShot
{
PROJECTILE
+NOTARGETSWITCH
+FORCEXYBILLBOARD
+SHOOTABLE
+MTHRUSPECIES
+THRUACTORS
+SLIDESONWALLS
+BOUNCEONWALLS
+BOUNCEONCEILINGS
+BOUNCEONFLOORS
+BRIGHT
Health 100
Obituary "$OB_BLACKHOLEBOMB"
Radius 15
Height 8
scale 2.5
damage (0)
damagetype "BlackHoleBomb"
speed 35
args 35
States
{
Spawn:
WABU D 0
WABU D 0 ACS_NamedExecuteAlways("GiveActorXrayTID",0,1)
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
WABU D 0 A_PlaySoundEx("weapon/waveburner","Weapon",1)
WABU D 1 A_JumpIfInTargetInventory("FlashWispFlag",2,"Switch")
WABU D 0 A_SpawnItemEx("FlashWispTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
WABU D 1 A_JumpIfInTargetInventory("FlashWispFlag",2,"Switch")
WABU D 0 A_SpawnItemEx("FlashWispTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
WABU D 0 ACS_ExecuteAlways(259, 0, 1)
WABU D 0 A_ChangeVelocity(cos(-pitch)*Args[0], 0, sin(-pitch)*Args[0], CVF_RELATIVE|CVF_REPLACE)
WABU D 0 A_PlaySoundEx("weapon/waveburner","Weapon",1)
WABU D 1 A_JumpIfInTargetInventory("FlashWispFlag",2,"Switch")
WABU D 0 A_SpawnItemEx("FlashWispTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
WABU D 1 A_JumpIfInTargetInventory("FlashWispFlag",2,"Switch")
WABU D 0 A_SpawnItemEx("FlashWispTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
WABU D 0 ACS_ExecuteAlways(259, 0, 1)
WABU D 0 A_ChangeVelocity(cos(-pitch)*Args[0], 0, sin(-pitch)*Args[0], CVF_RELATIVE|CVF_REPLACE)
WABU D 0 A_GiveToTarget("FlashWispActiveFlag",1)

loop
Switch:
WABU D 0 A_StopSound
WABU D 1 A_JumpIfInTargetInventory("FlashWispFlag",3,"Death")
WABU D 0 A_SpawnItemEx("FlashWispBlast",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
stop
Death:
WABU D 1 
WABU D 0 A_TakeFromTarget("FlashWispFlag",999)
WABU D 0 A_TakeFromTarget("FlashWispActiveFlag",999)
WABU D 0 A_StopSound
Dying:
TNT1 AAAA 2 A_SpawnItemEx("IncinerateParticleFX",Random(-32,32),0,0,0,0,6,Random(1,360),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
stop
}
}

actor FlashWispBlast : BasicExplosion2
{
-SOLID
+NOGRAVITY
PROJECTILE
+DONTBLAST
+NOINTERACTION
damage 0
radius 1
height 1
damagetype "FlashStopper"
Obituary "$OB_FLASHBANG"
States
{
Spawn:
TNT1 A 0
TNT1 A 5 A_SpawnItemEx("FlashPopFX1",0,0,0)
TNT1 A 0 A_PlaySoundEx("weapon/centaurflash","Weapon")
//TNT1 A 0 ACS_NamedExecuteAlways("CSLT_RadiusGive",0,RGI_FlashWisp,FlashRang,RG_ENEMY|RG_LOS)
TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("CSLT_flashstopperlight",FlashRang,175) > 0,"Spawn2")
TNT1 A 1
stop

Spawn2:
TNT1 A 1 A_GiveToTarget("FlashPingSoundCue")
stop
}
}

actor FlashWispTrail : BasicClientside
{
//translation "192:192=218:218", "198:198=221:221"
Renderstyle "Translucent"
scale 4.5
Alpha 0.7
States
{
Spawn:
TNT1 A 0 
WABU CBA 2
stop
}
}

actor FlashPopFX1 : BasicClientside
{
scale 1.5
ReactionTime 15
States
{
Spawn:
TNT1 A 0 
NDFX A 1
NDFX AA 1 A_SetScale(ScaleX+1.5)
Spawn2:
NDFX A 1 A_SetScale(ScaleX+1.5)
NDFX A 0 A_Fadeout(0.3)
NDFX A 0 A_CountDown
Loop
}
}


actor RadiusGiveItem_2 : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_GiveInventory("XrayTagItem")
stop
}
}

actor FlashPingSoundCue : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
ROLO R 0 A_PlaySoundEx("misc/BombCount","Weapon")
stop

Nope:
ROLO R 0 
stop

}
}