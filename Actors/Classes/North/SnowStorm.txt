actor Ability4_North : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 2
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "SmokeScreen Gun"
//weapon.ammotype2 "SmokeScreenAmmo"
inventory.icon "BLIZSI"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
NORT A 0 
"####" B 1 A_WeaponReady(WRF_NOSWITCH)
"####" B 0 A_FireCustomMissile("SSGuardRingSpawner",0,0,0,Aim_OverHead)
goto ready

Fire:
"####" CDE 1
"####" E 0 A_PlaySoundEx("weapon/freezecracker","Weapon")
"####" E 0 A_GiveInventory("SnowStormTimer",20)
"####" B 0 A_FireCustomMissile("SSGuardSpawner",0,1,0,Aim_OverHead)
"####" E 1 A_TakeInventory("UltimateCharge",99)
"####" E 0 A_GiveInventory("AmmoStaller")
"####" E 0 A_GiveInventory("SignatureRecharge_20") //
"####" E 7
"####" E 0 A_Jump(256,"End")
goto End

AltFire:
"####" E 1
End:
"####" E 1 A_TakeInventory("AbilityActiveFlag",9)
"####" E 0 
Goto End
}
}
//====

actor UseAbility4_North : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("SnowStorm_Ult",1)
fail
}
}

actor SnowStorm_Ult : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("UltimateCharge",6,"Pickup2")
stop

Pickup2:
TNT1 A 0 ACS_NamedExecuteWithResult("CSLT_Chatline",CLT_ABILITY,14)
TNT1 A 0 ACS_NamedExecuteWithResult("CSLT_Chatline",CLT_ABILITY,15,1) //Enemy Message
TNT1 A 0 A_PlaySoundEx("misc/chat_yell","SoundSlot8")
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,4)
stop
}
}

//=================
//=================


//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
const int SSGuard2_PARTMAX = 18;
const int WGA = 15;
const int WG_ExpandTime = 20;
const int SSGuardRad = WGA*WG_ExpandTime;
const int SSGuardEntiTID = 14;


Actor SnowStormProtect : powerprotection
{
Powerup.duration 7
Damagefactor "SnowStorm", 0
}

actor SnowStormWhirlProjectile : BasicProjectile
{  
-SERVERSIDEONLY
reactiontime 40
DamageType "SnowStorm"
obituary "$OB_SnowStorm"
scale 0.8
speed 7
+RIPPER
+NOINTERACTION
+NOCLIP
var int user_speed;
var int user_maxturn;
var int user_turndivisor;
var int user_turndivisor_add;
var int user_Rad;
States
{
Spawn:
POWS A 0
TNT1 A 0 A_SetUserVar("user_Rad", random(SSGuardRad-150,SSGuardRad+50))
TNT1 A 0 A_SetUserVar("user_turndivisor", 9)
TNT1 A 0 A_SetUserVar("user_speed", ((2*(3.14)*user_Rad)/360)*user_turndivisor)
TNT1 A 0 A_Warp(AAPTR_DEFAULT,user_Rad,0,0,random(0,360),WARPF_NOCHECKPOSITION)
TNT1 A 0 A_Setangle(angle-90)
TNT1 A 0 A_SetScale(Frandom(1.5,3.5))
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_Jump(256,"Spawn1","Spawn2")
Goto Spawn1

Spawn1:

FX03 A 0 A_SetTranslucent(0.5, 1)
Goto Spawn.Orbit

Spawn2:
FX03 B 0 A_SetTranslucent(0.5, 1)
Goto Spawn.Orbit

Spawn3:
ICEL M 0
Goto Spawn.Orbit

Spawn4:
ICEL O 0
Goto Spawn.Orbit

Spawn.Orbit:
"####" "#" 0 A_Setangle(angle-user_turndivisor)
"####" "#" 1 A_ChangeVelocity(user_speed,0,0,CVF_RELATIVE|CVF_REPLACE)
"####" "#" 0 A_CountDown
loop
Death:
POWS A 0
stop
}
}


actor SnowStormTimer : Ammo {inventory.amount 1 inventory.Maxamount 20}


actor SSGuard2Spawner
{
    radius 1
    height 1
    +MISSILE
    +NOINTERACTION
    var int user_part;
    var int user_InFloor;
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TARGET)
        BHOL B 0 ACS_NamedExecuteAlways("GiveEntiTID",0,SSGuardEntiTID)
        TNT1 A 3
        TNT1 A 0 A_SetUserVar(user_InFloor, (z-floorz < 32))
        TNT1 A 0 A_SpawnItemEx("SSGuard2Core", 0, 0, !user_InFloor * -32 + user_InFloor * -(z-floorz), 0, 0, 0, 0, SXF_TRANSFERPOINTERS,0,TID)
        TNT1 A 0 A_SpawnItemEx("SnowStormTunnel", 0, 0, !user_InFloor * -32 + user_InFloor * -(z-floorz), 0, 0, 0, 0, SXF_TRANSFERPOINTERS,0,TID)
        TNT1 A 0 A_SetUserVar(user_part, SSGUARD2_PARTMAX)
    SpawnWait:
        TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("core_targetexists")==0,"End")
        TNT1 A 1 A_JumpIfInTargetInventory("WideScreenKiller", 1, "Detonate")
        TNT1 A 0 A_SetArg(0,args[0]+1)
        TNT1 A 0 A_JumpIf(args[0] < 35,"SpawnWait")
        TNT1 A 0 A_TakeFromTarget("SnowStormTimer",1)
        TNT1 A 0 A_SetArg(0,0)
        TNT1 A 0 A_JumpifinTargetInventory("SnowStormTimer",1,"SpawnWait")
        TNT1 A 0 A_PlaySoundEx("weapon/BlackHolestart","auto")
        Goto End
    Detonate:
        TNT1 A 0 A_PlaySoundEx("weapon/BlackHolestart","auto")
        TNT1 A 15
        goto End
        /*
        TNT1 A 0 A_PlaySoundEx("weapon/BlackHoleexplode","auto")
        TNT1 A 1
    SpawnSpiral:
        TNT1 A 0 A_SpawnItemEx("SSGuard2Spiral", 0, 0, user_InFloor * (z-floorz), 1, 0, 0, user_part*(360/SSGUARD2_PARTMAX))
        TNT1 A 0 A_SetUserVar(user_part, user_part-1)
        TNT1 A 0 A_JumpIf(user_part>0, "SpawnSpiral")
        TNT1 A 1 A_JumpIf(true, "End")
        wait
        */
      End:
        TNT1 A 0 A_GiveToTarget("WideScreenKiller")
        TNT1 A 0 A_TakeFromTarget("WideScreenActive",99)
        TNT1 A 5
        stop
    }
}

actor SSGuard2Core
{
    +NOGRAVITY
    +ISMONSTER
    +NOTARGETSWITCH
    +THRUACTORS
    +FORCEXYBILLBOARD
    +MOVEWITHSECTOR
    +BRIGHT
    height 1
    radius 1
    maxstepheight 0
    renderstyle translucent
    alpha 0.8
    Scale 2.5
    var int user_part;
    var int user_partmax;
    States
    {
    Spawn:
        BHOL B 0
        WGUA C 0 A_RearrangePointers(AAPTR_TRACER, AAPTR_DEFAULT, AAPTR_DEFAULT)
        BHOL B 0 A_SetUserVar(user_partmax, 32)
        BHOL B 0 A_SetUserVar(user_part, user_partmax)
    SpawnParts:
    BLTR C 0 A_Jumpifintargetinventory("LightTeamFlag",1,"SpawnBLUE")
    BLTR C 0 A_Jumpifintargetinventory("WilyTeamFlag",1,"SpawnRED")
    BLTR C 0 A_Jumpifintargetinventory("CossackTeamFlag",1,"SpawnGREEN")
    BLTR C 0 A_Jumpifintargetinventory("KingTeamFlag",1,"SpawnYELLOW")

    BHOL B 0 A_SpawnItemEx("SSGuard2Part", 0, 0, 0, 0, 0, 0, user_part*(360/SSGUARD2_PARTMAX), SXF_SETMASTER|SXF_TRANSFERPOINTERS,0,tid)
    BHOL B 0 A_SpawnItemEx("SSGuard2Part2", 0, 0, 32, 0, 0, 0, user_part*(360/SSGUARD2_PARTMAX), SXF_SETMASTER|SXF_TRANSFERPOINTERS,0,tid)
    BHOL B 0 A_SpawnItemEx("SSGuard2Part", 0, 0, 64, 0, 0, 0, user_part*(360/SSGUARD2_PARTMAX), SXF_SETMASTER|SXF_TRANSFERPOINTERS,0,tid)
    goto SpawnPartsDone

    SpawnBLUE:
    BHOL B 0 A_SpawnItemEx("SSGuard2Part_B", 0, 0, 0, 0, 0, 0, user_part*(360/SSGUARD2_PARTMAX), SXF_SETMASTER|SXF_TRANSFERPOINTERS,0,tid)
    BHOL B 0 A_SpawnItemEx("SSGuard2Part2_B", 0, 0, 32, 0, 0, 0, user_part*(360/SSGUARD2_PARTMAX), SXF_SETMASTER|SXF_TRANSFERPOINTERS,0,tid)
    BHOL B 0 A_SpawnItemEx("SSGuard2Part_B", 0, 0, 64, 0, 0, 0, user_part*(360/SSGUARD2_PARTMAX), SXF_SETMASTER|SXF_TRANSFERPOINTERS,0,tid)
    goto SpawnPartsDone

    SpawnRED:
    BHOL B 0 A_SpawnItemEx("SSGuard2Part_r", 0, 0, 0, 0, 0, 0, user_part*(360/SSGUARD2_PARTMAX), SXF_SETMASTER|SXF_TRANSFERPOINTERS,0,tid)
    BHOL B 0 A_SpawnItemEx("SSGuard2Part2_r", 0, 0, 32, 0, 0, 0, user_part*(360/SSGUARD2_PARTMAX), SXF_SETMASTER|SXF_TRANSFERPOINTERS,0,tid)
    BHOL B 0 A_SpawnItemEx("SSGuard2Part_r", 0, 0, 64, 0, 0, 0, user_part*(360/SSGUARD2_PARTMAX), SXF_SETMASTER|SXF_TRANSFERPOINTERS,0,tid)
    goto SpawnPartsDone

    SpawnGREEN:
    BHOL B 0 A_SpawnItemEx("SSGuard2Part_g", 0, 0, 0, 0, 0, 0, user_part*(360/SSGUARD2_PARTMAX), SXF_SETMASTER|SXF_TRANSFERPOINTERS,0,tid)
    BHOL B 0 A_SpawnItemEx("SSGuard2Part2_g", 0, 0, 32, 0, 0, 0, user_part*(360/SSGUARD2_PARTMAX), SXF_SETMASTER|SXF_TRANSFERPOINTERS,0,tid)
    BHOL B 0 A_SpawnItemEx("SSGuard2Part_g", 0, 0, 64, 0, 0, 0, user_part*(360/SSGUARD2_PARTMAX), SXF_SETMASTER|SXF_TRANSFERPOINTERS,0,tid)
    goto SpawnPartsDone

    SpawnYELLOW:
    BHOL B 0 A_SpawnItemEx("SSGuard2Part_y", 0, 0, 0, 0, 0, 0, user_part*(360/SSGUARD2_PARTMAX), SXF_SETMASTER|SXF_TRANSFERPOINTERS,0,tid)
    BHOL B 0 A_SpawnItemEx("SSGuard2Part2_y", 0, 0, 32, 0, 0, 0, user_part*(360/SSGUARD2_PARTMAX), SXF_SETMASTER|SXF_TRANSFERPOINTERS,0,tid)
    BHOL B 0 A_SpawnItemEx("SSGuard2Part_y", 0, 0, 64, 0, 0, 0, user_part*(360/SSGUARD2_PARTMAX), SXF_SETMASTER|SXF_TRANSFERPOINTERS,0,tid)
    goto SpawnPartsDone

    SpawnPartsDone:
    BHOL B 0 A_SetUserVar(user_part, user_part-1)
        BHOL B 0 A_JumpIf(user_part<=0, "Active")
        TNT1 A 1 A_JumpIf(true, "SpawnParts")
        wait
    Active:
        TNT1 AAAA 1 A_JumpIfInTargetInventory("WideScreenKiller", 1, "Detonate")
        TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("core_targetexists")==0,"End")
        loop
    Detonate:
        BHOL A 0 A_KillChildren
        TNT1 AAAAAAAAAAAAAAAA 1
        stop
    End:
        TNT1 A 0
        stop
    }
}

actor SSGuard2Part
{
    +ISMONSTER
    +NOCLIP
    +NOGRAVITY
    +DROPOFF
    +MISSILE
    +ACTIVATEIMPACT
    +ACTIVATEPCROSS
    +SHOOTABLE
    +DONTREFLECT
    +DONTSPLASH
    +DONTBLAST
    +DONTRIP
    //+INVULNERABLE
    +NOBLOOD
    +FORCEXYBILLBOARD
    +MOVEWITHSECTOR
    +NOTAUTOAIMED
    +BRIGHT
    +MTHRUSPECIES
    +GHOST
    Renderstyle "Add"
    painchance 0
    DamageFactor "normal", 0.0
    DamageFactor "BrickBreak", 1.0
    painchance "BrickBreak", 256
    maxstepheight 0
    Scale 0.5
    height 56
    radius 38
    var int user_radius;
    var int user_angle;
    var int user_angleadd;
    var int user_expandtime;
    var int user_Z;
    States
    {
    Spawn:
        WGUA C 0
        WGUA C 0 A_SetUserVar(user_radius, 32)
        WGUA C 0 A_SetUserVar(user_angleadd, 12)
        WGUA C 0 A_SetUserVar(user_expandtime, WG_ExpandTime)
        WGUA C 0 A_SetUserVar(user_angle, angle)
        WGUA C 0 A_SetUserVar(user_Z, z-floorz)
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        WGUA C 0 A_Jump(256,"Expand")
        goto Expand
    SpawnLoop:
        WGUA B 0 A_JumpIf(ACS_NamedExecuteWithResult("core_targetexists")==0,"End")
        WGUA B 0 A_JumpIfInTargetInventory("WideScreenKiller", 1, "Detonate")
        WGUA C 0 A_SetUserVar(user_angle, user_angle-user_angleadd)
        WGUA C 1 A_Warp(AAPTR_MASTER, cos(-user_angle)*user_radius, sin(-user_angle)*user_radius, user_Z, 0, WARPF_ABSOLUTEANGLE|WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION) 
        WGUA B 0 A_JumpIfInTargetInventory("WideScreenKiller", 1, "Detonate")
        WGUA C 0 A_SetUserVar(user_angle, user_angle-user_angleadd)
        WGUA C 1 A_Warp(AAPTR_MASTER, cos(-user_angle)*user_radius, sin(-user_angle)*user_radius, user_Z, 0, WARPF_ABSOLUTEANGLE|WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)  
        WGUA B 0 A_JumpIfInTargetInventory("WideScreenKiller", 1, "Detonate")
        WGUA B 0 A_SetUserVar(user_angle, user_angle-user_angleadd)
        WGUA B 1 A_Warp(AAPTR_MASTER, cos(-user_angle)*user_radius, sin(-user_angle)*user_radius, user_Z, 0, WARPF_ABSOLUTEANGLE|WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
        WGUA B 0 A_JumpIfInTargetInventory("WideScreenKiller", 1, "Detonate")
        WGUA B 0 A_SetUserVar(user_angle, user_angle-user_angleadd)
        WGUA B 1 A_Warp(AAPTR_MASTER, cos(-user_angle)*user_radius, sin(-user_angle)*user_radius, user_Z, 0, WARPF_ABSOLUTEANGLE|WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)  
        loop
    Expand:
        WGUA B 0 A_SetUserVar(user_angleadd, user_angleadd-((user_expandtime%4)%2))
		WGUA B 0 A_SetUserVar(user_radius, user_radius+WGA)
		WGUA B 0 A_SetUserVar(user_expandtime, user_expandtime-1)
		WGUA B 1
		WGUA B 0 A_SetUserVar(user_angle, user_angle-user_angleadd)
		WGUA B 0 A_Warp(AAPTR_MASTER, cos(-user_angle)*user_radius, sin(-user_angle)*user_radius, user_Z, 0, WARPF_ABSOLUTEANGLE|WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
		WGUA B 0 A_JumpIf(user_expandtime <= 0, "SpawnLoop")
      WGUA B 0 A_JumpIfInTargetInventory("WideScreenKiller", 1, "Detonate")
          loop
    Pain.BrickBreak:
    WGUA B 0 A_GiveToTarget("WideScreenKiller",1)
    Death:
    Detonate:
        WGUA B 0 A_SetUserVar(user_radius, user_radius-16)
        WGUA B 1
        WGUA B 0 A_SetUserVar(user_angle, user_angle-user_angleadd)
        WGUA B 0 A_Warp(AAPTR_MASTER, cos(-user_angle)*user_radius, sin(-user_angle)*user_radius, user_Z, 0, WARPF_ABSOLUTEANGLE|WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
        WGUA B 0 A_JumpIf(user_radius <= 0, "End")
        loop
    End:
        WGUA B 0
        stop
    }
}


actor SSGuard2Part2 : SSGuard2Part
{
    States
    {
    Spawn:
        WGUA C 0
        WGUA C 0 A_SetUserVar(user_radius, 32)
        WGUA C 0 A_SetUserVar(user_angleadd, -12)
        WGUA C 0 A_SetUserVar(user_expandtime, WG_ExpandTime)
        WGUA C 0 A_SetUserVar(user_angle, angle)
        WGUA C 0 A_SetUserVar(user_Z, z-floorz)
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        WGUA C 0 A_Jump(256,"Expand")
        goto Expand
    Expand:
        WGUA B 0 A_SetUserVar(user_angleadd, user_angleadd+((user_expandtime%4)%2))
        WGUA B 0 A_SetUserVar(user_radius, user_radius+WGA)
        WGUA B 0 A_SetUserVar(user_expandtime, user_expandtime-1)
        WGUA B 1
        WGUA B 0 A_SetUserVar(user_angle, user_angle-user_angleadd)
        WGUA B 0 A_Warp(AAPTR_MASTER, cos(-user_angle)*user_radius, sin(-user_angle)*user_radius, user_Z, 0, WARPF_ABSOLUTEANGLE|WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
        WGUA B 0 A_JumpIf(user_expandtime <= 0, "SpawnLoop")
        WGUA B 0 A_JumpIfInTargetInventory("WideScreenKiller", 1, "Detonate")
        

        loop
    }
  }


  Actor SSGuard2Part_B : SSGuard2Part{Designatedteam 0 Species "LightMember" }
  Actor SSGuard2Part_R : SSGuard2Part{Designatedteam 1 Species "WilyMember" }
  Actor SSGuard2Part_G : SSGuard2Part{Designatedteam 2 Species "CossackMember" }
  Actor SSGuard2Part_Y : SSGuard2Part{Designatedteam 3 Species "KingMember" }

  Actor SSGuard2Part2_B : SSGuard2Part2{Designatedteam 0 Species "LightMember" }
  Actor SSGuard2Part2_R : SSGuard2Part2{Designatedteam 1 Species "WilyMember" }
  Actor SSGuard2Part2_G : SSGuard2Part2{Designatedteam 2 Species "CossackMember" }
  Actor SSGuard2Part2_Y : SSGuard2Part2{Designatedteam 3 Species "KingMember" }

  actor SSGuardRingGuide : BasicHelper
  {
  translation "198:198=220:220", "192:192=229:229"
    reactiontime 15
    var int user_angle;
    States
    {
      Spawn:
      TNT1 A 0
      TNT1 A 0 //A_SetUserVar("user_angle", random(0, 30))
      TNT1 A 0 A_JumpIf(!CallACS("Arc_ClientSide"),"Perish")
      SpawnLoop:
      TNT1 A 0 A_SpawnItemEx("SSGuardGuideDot", SSGuardRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)
      TNT1 A 0 A_SpawnItemEx("SSGuardGuideDot", -SSGuardRad, 0, -(z-floorz)+24, 0, 0, 0, user_angle, 1061)

      TNT1 A 0 A_SpawnItemEx("SSGuardGuideDot", SSGuardRad, 0, -(z-floorz)+96, 0, 0, 0, user_angle, 1061)
      TNT1 A 0 A_SpawnItemEx("SSGuardGuideDot", -SSGuardRad, 0, -(z-floorz)+96, 0, 0, 0, user_angle, 1061)
      TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
      TNT1 A 0 A_Countdown
      loop
      Death:
      TNT1 A 0
      stop
    }
  }

  actor SSGuardRingSpawner : FastProjectile
  {
  translation "198:198=220:220", "192:192=229:229"
  +DONTBLAST
  +SKYEXPLODE
  +THRUACTORS
  +CLIENTSIDEONLY
  reactiontime 1
  damage (0)
  radius 3
  height 3
  speed 1
  States
  {
  Spawn:
  TNT1 A 0
  TNT1 A 1 A_CountDown
  loop
  Death:
  TNT1 A 0 A_Jumpif(z-floorz > 175,"Terminate")
  TNT1 A 0 A_SpawnItemEx("SSGuardRingGuide",0,0,0,0,0,0,0,1)
  stop
  
  Terminate:
  TNT1 A 0 
  stop
  }
  }

  actor SSGuardSpawner : FastProjectile
  {
  +DONTBLAST
  +SKYEXPLODE
  +THRUACTORS
  reactiontime 1
  damage (0)
  radius 3
  height 3
  speed 1
  States
  {
  Spawn:
  TNT1 A 0
  TNT1 A 0 A_GiveInventory("ProjectileCode",1)
  TNT1 A 1 A_CountDown
  loop
  Death:
  TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
  //SCBL A 0 A_PlaySoundEx("weapon/Crashland","Weapon")
  SCBL A 2 ACS_NamedExecuteAlways("WideScreenKiller",0)//
  TNT1 A 0 A_Givetotarget("WideScreenActive")
  TNT1 A 0 A_SpawnitemEx("SSGuard2Spawner",0,0,-(z-floorz),0,0,0,0,SXF_TRANSFERTRANSLATION)
  stop
  
  Terminate:
  TNT1 A 0 
  stop
  }
  }

actor SSGuardGuideDot : BasicGuideDot {}



actor SnowStormTunnel
{
PROJECTILE
+NOINTERACTION
+FORCEXYBILLBOARD
+RIPPER
+OLDRADIUSDMG
damage (0)
Obituary "$OB_SNOWSTORM"
Height 1
Radius 1
damagetype "SnowStorm"
reactiontime 12
Args 25
Scale 4.2

Renderstyle "Translucent"
Alpha 0.1

States
{
Spawn:
TBLO E 0
TBLO E 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SetTics(WG_ExpandTime)
TBLO E 0 A_PlaySoundEx("misc/commandosandstorm", "body", 1)
TBLO E 0 A_PlaySoundEx("Move/Blizzard", "SoundSlot6")
Spawn2:
TNT1 A 0 A_RadiusGive("SnowStormHeightGuard",SSGuardRad,RGF_PLAYERS|RGF_MONSTERS|RGF_OBJECTS)
TNT1 A 5 A_Explode(2,SSGuardRad,0,0,SSGuardRad)
TNT1 AAA 0 A_SpawnItemEx("SnowStormWhirlProjectile", 0, 0, Random(48,56), 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
TNT1 A 1 A_JumpifinTargetInventory("SnowStormTimer",1,"Spawn2")
goto Death
Death:
TNT1 A 0 A_StopSound
stop
}
}

actor SnowStormHeightGuard : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIF(Floorz-z < SSGuardRad,"Nope")
TNT1 A 0 A_GiveInventory("SnowStormOutrange",1)
stop

Nope:
TNT1 A 0
stop
}
}

actor SnowStormOutrange : PowerProtection
{
Powerup.Duration 2
damagefactor "SnowStorm", 0.0
}

//[+]========================================================================[+]
//--------------------------------|Shield Inventories|---------------------------
//[+]========================================================================[+]
//#region 

actor WideScreenActive : inventory {inventory.amount 1 inventory.maxamount 1}
actor WideScreenKiller : powerup {inventory.amount 1 inventory.maxamount 1 Powerup.duration 5}
