actor BasicMolotovWep : WepMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 2
Weapon.SlotNumber 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "Basic Molotov"
inventory.icon "BUSTI"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
HSPR B 1 A_WeaponReady
"####" B 0 A_FireCustomMissile("ThrowableAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" "#" 0 
Goto Ready

fire:
"####" FFGGG 1
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("BasicMollyBall",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" IJKLM 2
Hold:
"####" B 1
"####" B 0 A_Refire
goto Ready

}
}

Const Int IncArea = 150;
Const int Molly_EntiTID = 4;

actor BasicMollyBall : BasicThrowable
{
States
{

Spawn2:
MBAL B 1 A_SpawnItemEX("BasicThrowableTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
MBAL B 0 A_CountDown
TNT1 A 1 A_Jump(256,"Spawn2")
Goto Spawn2

Spawn.Bounced:
BOMB S 1 A_SpawnItemEX("BasicMollyBall2",0,0,0,momx,momy,momz,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM,0)
stop

Bounce.Floor:
BOMB S 1 A_SpawnItemEX("MolotovField",0,0,0,0,0,0,0,1,0)
stop

Death:
MINB A 0 A_SpawnItemEx("ExplosionEffect1",0,0,0,0,0,0,0,1)
stop
}
}

actor BasicMollyBall2 : BasicMollyBall
{
-USEBOUNCESTATE
-NOGRAVITY
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_Jump(256,"Spawn2")
Goto Spawn2

Spawn2:
MBAL B 1 A_CountDown
MBAL B 0 A_CheckFloor("Death")
loop
	
Death:
MINB A 0 A_SpawnItemEx("MolotovField",0,0,0,0,0,0,0,1)
stop
}
}

actor MolotovField : BasicExplosion2
{
DamageType "Molotov"
obituary "$OB_MOLOTOV"
ReactionTime 35
+OLDRADIUSDMG
Args 0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCodeSyncTarget",1)
TNT1 A 0 ACS_NamedExecuteAlways("GiveEntiTID",0,Molly_EntiTID) //If another actor is spawned sharing this EntiTID, the first one is removed
TNT1 A 5
TNT1 A 0 A_Explode(5,IncArea,1,0,IncArea)
Spawn2:
TNT1 A 1
TNT1 A 0 A_PlaySoundEx("weapon/WaveBurner","Weapon")
TNT1 AAA 0 A_SpawnItemEx("MolotovParticleSpawner",IncArea,0,-(z-floorz)+random(0,5),0,0,6,Random(1,360),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 AAA 0 A_SpawnItemEx("MolotovParticleSpawner",Random(-IncArea,IncArea),0,-(z-floorz)+random(0,5),0,0,6,Random(1,360),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SetArg(0,Args[0]+1)
TNT1 A 0 A_JumpIf(Args[0] >= 7,"Spawn3")
Goto Spawn2
Spawn3:
TNT1 A 0 A_RadiusGive("MolotovOffGround",IncArea,RGF_PLAYERS|RGF_MONSTERS|RGF_OBJECTS)
TNT1 A 0 A_Explode(5,IncArea,1,0,IncArea)
TNT1 A 0 A_SetArg(0,0)
TNT1 A 0 A_CountDown
Goto Spawn2
Death:
TNT1 A 0 
stop
}
}


actor MolotovOffGround : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_CheckFloor("Nope")
TNT1 A 0 A_GiveInventory("MolotovNoGround",1)
stop

Nope:
TNT1 A 0
stop
}
}

actor MolotovNoGround : PowerProtection
{
Powerup.Duration 2
damagefactor "Molotov", 0.0
}

actor MolotovParticleSpawner : BasicHelper
{
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 1 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 A_SpawnItemEx("IncinerateParticleFX",0,0,-(z-floorz),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        stop
    }

}

actor IncinerateParticleFX : BasicClientside
{
//translation "192:192=218:218", "198:198=221:221"
Renderstyle "add"
scale 1.5
Alpha 0.7
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_SetScale(Frandom(1.0,2.0))
BRNF ABCDEFGH 1 A_ScaleVelocity(0.6)
stop
}
}