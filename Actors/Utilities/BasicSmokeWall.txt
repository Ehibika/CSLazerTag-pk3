actor BasicSmokeLineWep : WepMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 2
Weapon.SlotNumber 1
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "Basic SmokeLine"
inventory.icon "BUSTI"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
HSPR B 1 A_WeaponReady
"####" B 0 A_FireCustomMissile("ThrowableAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" "#" 0 
Goto Ready

fire:
"####" FFGGG 1
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("BasicSmokeLine",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" IJKLM 2
Hold:
"####" B 1
"####" B 0 A_Refire
goto Ready

}
}

Const int SmokeLineRad = 75;

actor BasicSmokeLine : BasicThrowable
{
States
{

Spawn2:
MBAL B 1 A_SpawnItemEX("BasicThrowableTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
MBAL B 0 A_CountDown
TNT1 A 1 A_Jump(256,"Spawn2")
Goto Spawn2

Spawn.Bounced:
BOMB S 1 A_SpawnItemEX("BasicSmokeLine2",0,0,0,momx,momy,momz,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM,0)
stop

Bounce.Floor:
BOMB S 1 A_SpawnItemEX("SmokeLineMaker",0,0,0,20,0,0,0,1,0)
stop

Death:
MINB A 0 A_SpawnItemEx("ExplosionEffect1",0,0,0,0,0,0,0,1)
stop
}
}

actor BasicSmokeLine2 : BasicSmokeLine
{
-USEBOUNCESTATE
-NOGRAVITY
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_Jump(256,"Spawn2")
Goto Spawn2

Spawn2:
MBAL B 1 A_CountDown
MBAL B 0 A_CheckFloor("Death")
loop
	
Death:
MINB A 0 A_SpawnItemEx("SmokeLineMaker",0,0,0,20,0,0,0,1)
stop
}
}

actor SmokeLineMaker
{
PROJECTILE
+RIPPER
+DONTREFLECT
+FLOORHUGGER
+SLIDESONWALLS
Damage (0)
Speed 20
Height 48
Radius 10
Scale 2.5
ReactionTime 15
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
TNT1 A 0 A_SpawnItemEx("SmokeLinePartSpot")
TNT1 AA 1 
TNT1 A 0 A_CountDown
Goto Spawn2
Death:
TNT1 A 0 
stop
}
}


actor SmokeLinePartSpot : BasicHelper
{
ReactionTime 35
states
{
Spawn:
TNT1 A 0
TNT1 A 2 A_GiveInventory("ProjectileCodeSyncTarget",1)
TNT1 AAAAA 2 A_SpawnItemEx("SmokeLinePuffFX",random(-16,16),random(-16,16),0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS,0,tid)
TNT1 A 0 A_SpawnItemEx("SmokeLineFX",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS,0,tid)
TNT1 AAA 4
Goto Spawnloop
//It's important to ensure that this actor and the visual FX actor "SmokeLineFX" exist and expire at around the same time.
SpawnLoop:
TNT1 AAAA 3 A_SpawnItemEx("SmokeLineBlinderTower")
TNT1 A 0 A_CountDown
Loop 

Death:
TNT1 A 1 
stop
}
}


actor SmokeLineBlinderTower : BasicHelper
{
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("SmokeLineBlinder",0,0,SmokeLineRad,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS,0,tid)
TNT1 A 0 A_SpawnItemEx("SmokeLineBlinder",0,0,SmokeLineRad*2,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS,0,tid)
TNT1 A 0 A_SpawnItemEx("SmokeLineBlinder",0,0,SmokeLineRad*3,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS,0,tid)
stop
}
}

actor SmokeLineBlinder : BasicHelper
{
states
{
Spawn:
TNT1 A 0
TNT1 A 1 A_RadiusGive("SmokeLineBlind",SmokeLineRad,RGF_PLAYERS,1)
stop 
}
}

actor SmokeLineBlind : FlashStopperBlind
{
    States
    {
    Pickup:
        TNT1 A 0 A_JumpIfInventory("BrightBlind", 1,"Sustain")
		TNT1 A 0 A_SpawnItemEx("FlashBlindWatcher")
        TNT1 A 0 A_GiveInventory("BrightBlind",100)
        TNT1 A 0 ACS_ExecuteAlways(249, 0)
        goto End
		Sustain:
        TNT1 A 0 A_TakeInventory("BrightBlind",999)
        TNT1 A 0 A_GiveInventory("BrightBlind",100)
        goto End
    }
}



actor SmokeLineFX : BasicClientside
{
scale 2.5
Reactiontime 35
+FLOORHUGGER
+FORCEXYBILLBOARD
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
SMWL ABC 4 
Spawn2:
SMWL DC 6
TNT1 A 0 A_CountDown
loop
Death:
SMWL EFG 2 
stop
}
}

actor SmokeLinePuffFX : BasicClientside
{
scale 1.5
ReactionTime 3
States
{
Spawn:
TNT1 A 0
SMKC ABCDEFG 1 
stop
}
}